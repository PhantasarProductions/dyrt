module AG_AI
	
	var DefAI
	int turn 
	
	table Elements
	table Weak
	table Spell
	
	void AddE(string e,string w,string s)
		self.Elements[#self.Elements+1] = e
		self.Weak[e] = w
		self.Spell[e] = s
	end
	
	void CONSTRUCTOR()
		self.DefAI = NIL.Use("Script/Use/Specific/CombatModules/AI/Default")
		self.AddE("Flame","Frost","INFERNO")
		self.AddE("Wind","Earth","WIND")
		self.AddE("Water","Thunder","TSUNAMI")
		self.AddE("Earth","Wind","QUAKE")
		self.AddE("Frost","Flame","FROST")
		self.AddE("Thunder","Water","THUNDER")
		self.AddE("Light","Darkness","LIGHT")
		self.AddE("Darkness","Light","DARKNESS")
		self.turn = -1
	end
	
	void Decide(foe)
		self.turn++
		if self.turn % 3 != 0 
			self.DefAI.Decide(foe)
			return
		end
		int r
		string e
		string w
		string s
		var f
		r = math.random(1,#self.Elements)
		e = self.Elements[r]
		w = self.Weak[e]
		s = self.Spell[e]
		f = foe.Char
		for i,elem in ipairs(self.Elements) do
			if elem==w then
				f.Stat("BASE__RESIST_Elem_"..elem).Value=-math.random(600/__skill,60000/__skill)
			elseif elem==e then
				f.Stat("BASE__RESIST_Elem_"..elem).Value=math.random(50*__skill,10000*__skill)
			else
				f.Stat("BASE__RESIST_Elem_"..elem).Value=100 * (3 - __skill)
			end
		end
		table nact
		nact.ActTag = "ABL_FOE_X"..s
		nact.ExeTag = foe.Tag
		nact.Targets = {}
		table heroes
		for tag,hero in pairs(Fighter.Team.Hero)
			heroes[#heroes+1]=hero
		end
		for check in each(heroes)
			if check.Char.Points("HP").Have>0
				nact.Targets[#nact.Targets+1]=check
			end
		end
		foe.NextAct = new Actions(nact)
		foe.NextActSpeed = 3000 * __skill
		foe.GaugePosition = 10001
	end
	
end
