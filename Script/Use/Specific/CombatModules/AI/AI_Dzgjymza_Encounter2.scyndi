// License:
// **********************************************
// 
// src/Script/Script/Use/Specific/CombatModules/AI/AI_Dzgjymza_Encounter2.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.06.09 II
// 
// End License


module 

// DZGJYMZA_AI1

global var LordDzgjymza

	#use "Libs/SCI/Sys"
	
	#use "Script/Use/Need"
	
	#accept Fighter

	int beurt
	readonly table Actions
	
	get table Eric
		int r 
		r = math.random(1,4)
		for i=1,4
			if RPG.Party[i]=="Yasathar" && RPG.GetChar("Yasathar").Points("HP").Have>0
				r = i
			end
		end
		CSayF("Eric: Picked %d >> %s",r,RPG.Party[r] or "???")
		return { [0] = Fighter.ByTag[RPG.Party[r]] }
	end
	
	get table RandomTarget
		int r 
		r = math.random(1,4)
		CSayF("Random: Picked %d >> %s",r,RPG.Party[r] or "???")
		return { [0] = Fighter.ByTag[RPG.Party[r]] }
	end
	
	get table All
		table ret
		for i=1,4
			ret[i-1] = Fighter.ByTag[RPG.Party[i]] 
		end
		return ret
	end
	
	get table Everybody
		table ret
		ret = All
		ret += Fighter.ByTag.FOE_1
		return ret
	end
	
	Init
		for st in each { [0]="Strength", "Defense","Intelligence", "Resistance", "Agility", "Evasion", "HP","_RESIST_Elem_None","_RESIST_Elem_Flame","_RESIST_Elem_Wind","_RESIST_Elem_Water","_RESIST_Elem_Earth","_RESIST_Elem_Frost","_RESIST_Elem_Thunder","_RESIST_Elem_Light","_RESIST_Elem_Darkness" }
			int stat = 0
			for i=1,8
				stat += RPG.GetChar(RPG.Party[i]).Stat("END_"..st).Value
				CSayF("Build %d: %10s = %5d",i,st,stat)
			end
			if st!="HP"
				RPG.GetChar("FOE_1").Stat("BASE_"..st).Value = stat Div (7-(__skill))
			else
				RPG.GetChar("FOE_1").Stat("BASE_"..st).Value = stat * (__skill^2)
			end
		CSayF("End    : %10s = %5d => %5d ",st,stat,RPG.GetChar("FOE_1").Stat("BASE_"..st).Value)
		end
		Fighter.ByTag.FOE_1.Char.Points("HP").Have = Fighter.ByTag.FOE_1.Char.Points("HP").Maximum
		Fighter.ByTag.FOE_1.Char.Points("HP").Minimum = 1
		LordDzgjymza = Lua.Scyndi.Class.AI_Dzgjymza_Encounter2
	end
	
	void SetTurn(string Tag, Targets)
		var foe
		table nact
		var nitem
		assert(Len(Targets)>0,"No targets set for Dzgjymza's move \""..Tag.."\"???")
		foe = Fighter.ByTag.FOE_1
		nitem = Item[Tag]
		nact.ActTag = Tag
		nact.ExeTag = foe.Tag
		nact.Targets = Targets
		foe.NextAct = new Actions(nact)
		foe.NextActSpeed = nitem.COM2ACT
		foe.GaugePosition = 10001
	end

	void Decide(foe)		
		beurt = (beurt + 1 ) % 10
		switch beurt 
			case 0
				SetTurn("ABL_HERO_AZIELLA_BLUNT",RandomTarget)
			case 1
				if __skill==3
					SetTurn("ABL_FOE_DEMONSOULBREAKER",All)
				else
					SetTurn("BTY_OBSERVE",Eric)
				end
			case 2
				SetTurn( ({"ABL_HERO_IRRAVONIA_FIRE","ABL_HERO_IRRAVONIA_SPLASH","ABL_HERO_IRRAVONIA_BREEZE","ABL_HERO_IRRAVONIA_ROCK"})[math.random(1,4)],RandomTarget)
			case 3
				SetTurn( "ABL_HERO_AZIELLA_VANISH",Everybody)
			case 4
				if __skill==3
					SetTurn("ABL_FOE_ULTIMATESOULBREAKER",All)
				else
					SetTurn( "ZZZ_DZGJYMZA_CHARGE",{[0]=Fighter.ByTag.FOE_1})
				end
			case 5 
				SetTurn("ZZZ_DZGJYMZA_JUDGMENT",All)
			case 6
				SetTurn("ABL_HERO_SCYNDI_RECOVER",{[0]=Fighter.ByTag.FOE_1})
			case 7
				SetTurn("ABL_FOE_DEMONSOULBREAKER",All)
			case 8
				SetTurn("ABL_FOE_MALIFICATION",All)
			case 9
				SetTurn( ({"ABL_HERO_IRRAVONIA_FIREBLAST","ABL_HERO_IRRAVONIA_TSUNAMI","ABL_HERO_IRRAVONIA_HURRICANE","ABL_HERO_IRRAVONIA_QUAKE"})[math.random(1,4)],RandomTarget)
			default
				error("Unknown turn number: "..beurt)
		end
	end
	
//end
