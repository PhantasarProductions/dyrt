// License:
// **********************************************
// 
// src/Script/Script/Use/Specific/CombatModules/SpellAni/SpellAni_Inferno.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.06.03 V
// 
// End License

Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/Audio"
#use "Libs/SCI/Graphics"

#use "libs/linkedlist"

#use "Script/Use/Need"

class SInferno_Flame
	readonly int x
	readonly int y
	readonly int id
	int frame = 1
	readonly int skip
	int skipvalue
	static var List
	static int count
	static var Flame
	
	CONSTRUCTOR()
		self.x = math.random(1,Graphics.W)
		self.y = math.random(1,Graphics.H)
		self.count++
		self.id = self.count
		self.List.AddLast(self)
		self.skip = math.random(1,10)
		self.skipvalue = 0
		self.frame = 1
		CSayF("Inferno flame #%d created!",self.id)
	end
	
	DESTRUCTOR
		CSayF("Inferno flame #%d destroyed!",self.id)
	end
	
	bool Draw()
		self.skipvalue++
		if self.skipvalue>self.skip
			self.skipvalue=0
			self.frame++
		end
		if self.frame>=10
			return true;
		end
		self.Flame.Draw(self.x,self.y,self.frame)
		return false
	end
	
	static void DrawAll()
		for vlam,schakel in self.List.Each
			if vlam.Draw()
				schakel.UnLink()
				CSayF("Inferno flame #%d unlinked!",vlam.id)
			end
		end
	end
end
	
//module SpellAni_BurnBabyBurn_DiscoInferno

	link Flame = SInferno_Flame.Flame
	var SA_Audio
	
	
	void SA_Init()
		int aantal
		aantal = math.random(20,50)
		repeat
			--aantal
			new SInferno_Flame
		until aantal<=0
		SA_Audio.Play()
	end
	
	bool Cycle()
		Color(255,100,0)
		SetAlpha(95)
		Rect(0,0,Graphics.W,Graphics.H)
		SetAlpha(255)
		__white
		SInferno_Flame.DrawAll()
		return SInferno_Flame.List.Count==0
	end
	



Init
	Flame    = LoadNewImage("gfx/Combat/SpellAni/Flame.jpbf","SPELLANI_Flame")
	SA_Audio = Audio.LoadNew("SFX/Combat/SpellAni/Flame.wav","SPELLANI_Flame")
	SInferno_Flame.List = new TLinkedList()
end
