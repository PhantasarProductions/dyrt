// License Information:
// src/Tricky Script/Script/Use/Specific/CombatModules/Arena/Templates/Prison.nil
// Version: 19.12.24
// Copyright (C)  Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information

#use "libs/LinkedList"


#pure 
local
#endpure

class Torch
	int x
	int y
	var pic
	int spd
	int skip
	int Frame
	bool animate
	
	static var list
	
	void CONSTRUCTOR()
		self.list.AddLast(self)
	end
	
	void Draw()
		self.pic.Draw(self.x,(Screen.Height/2)-self.y,self.Frame)
		if self.animate
			self.skip++
			if self.skip>=self.spd
				self.skip=0
				int wtd
				wtd = math.random(1,10)
				switch wtd
					case 1
						if self.spd>7
							self.spd --
						end
					case 2
						if self.spd<12
							self.spd++
						end
				end
			end
			self.Frame++
			if (self.Frame>self.pic.Frames)
				self.Frame = 0
			end
		end
	end
	
end

class Skeleton
	static var List
	int x
	int y
	
	void CONSTRUCTOR(int x)
		int sy
		int ey
		ey = math.floor(Screen.Height/2)
		sy = ey - (4 * 32)
		self.x = x
		self.y = sy + math.random(0,2)
		self.List.AddLast(self)
	end
	
	void Draw()
		LoadIfNew("GFX/Textures/Prison/Alpha Skeleton.png","Arena_Skeleton").Draw(self.x,self.y)
	end
end


module Arena_Prison

	bool Torches = true
	bool TorchBurn = true
	bool TorchOff = true
	bool Skeleton = true
	get var ImgTorchBurn 
		return LoadIfNew("gfx/Textures/Objects/Toorts/Brandend.jpbf","DungArenaTorchBurning")
	end
	
	get var ImgTorchOff
		return LoadIfNew("gfx/Textures/Objects/Toorts/Uit.png"      ,"DungArenaTorchNotBurning")
	end
	
	link TorchList = Torch.list
	link SkeletonList = Skeleton.List
	
	string Wall
	string Floor
	
	var ImgWall
	var ImgFloor
	
	void CONSTRUCTOR()
		self.TorchList = new TLinkedList
		self.SkeletonList = new TLinkedList
	end
	
	void Init()
		assert(self.Wall!="" and self.Floor!="","Missing arena data!")
		self.ImgWall  = LoadImage(self.Wall,"CArenaDungWall")
		self.ImgFloor = LoadImage(self.Floor,"CArenaDungFloor")
		// The tags force a C# garbage collecting once other stuff is loaded!
		if self.Torches
			assert(
				self.TorchBurn or self.TorchOff,
				"Invalid torches setting. One of them must be true, or the torches in general must be turned off!"
			)
			int tx
			var trc
			int au
			tx = math.random(64,120)
			repeat			
				if self.Torches
					bool ok
					repeat
						au = math.random(1,5)
						switch au
							case 1 3
								if self.TorchBurn
									trc = new Torch
									trc.x = tx
									trc.y = math.random(20,50)
									trc.spd = math.random(6,12)
									trc.pic = self.ImgTorchBurn
									trc.animate = true
									ok = true
								end
							case 2 4
								if self.TorchOff
									trc = new Torch
									trc.x = tx
									trc.y = math.random(20,50)
									trc.spd = math.random(6,12)
									trc.pic = self.ImgTorchOff
									trc.animate = false
									ok = true
								end
							case 5
								if self.Skeleton
									new Skeleton(tx)
									ok = true
								end
						end
					until ok
				end
				tx = tx + math.random(60,200)
			until tx >= Screen.Width-64
		end
	end
	
	void Draw()
		int sy
		int ey
		ey = math.floor(Screen.Height/2)
		sy = ey - (4 * 32)
		self.ImgFloor.Tile(0,ey,Screen.Width,ey)
		for y = sy,ey,32
			self.ImgWall.Tile(0,y+32,Screen.Width,32)
		end
		for trc in Torch.list.Each
			trc.Draw()
		end
	end

end