// License Information:
// src/Tricky Script/Script/Use/Specific/FieldModules/ZoneAction/SchuifDeuren.nil
// Version: 20.01.10
// Copyright (C)  Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
#use "libs/linkedlist"
#use "libs/tables"

#pure
local
#endpure
class SchuifDeur

	link Kthura = FlowField.Map.Kthura

	bool open
	readonly int x
	readonly int y
	readonly string Richting
	readonly string Layer
	readonly string Name
	
	readonly static table Lijst
	
	void CONSTRUCTOR(string Layer,string NxPv)
		self.Lijst[Layer..NxPv] = self
		self.Name = NxPv
		self.Layer = Layer
		self.Kthura.Obj = NxPv
		self.x = self.Kthura.ObjInt.x
		self.y = self.Kthura.ObjInt.y
		if suffixed(NxPv,"Links")
			self.Richting = "Links"
		elseif suffixed(NxPv,"Rechts")
			self.Richting = "Rechts"
		else
			error("Unknown direction name: "..NxPv)
		end
		
	end
	
	void DESTRUCTOR()
		CSay("Schuifdeur entry "..self.Name.." has been removed from memory!")
	end
end

module AutomatischeSchuifDeuren
	
	readonly table scanlist
	
	link Kthura = FlowField.Map.Kthura
	
	void CONSTRUCTOR()
		self.scanlist = {"NextLinks","NextRechts","PrevLinks","PrevRechts"}
		new ZoneAction("Enter","SchuifNext",self.OpenNext)
		new ZoneAction("Enter","SchuifPrev",self.OpenPrev)
		new ZoneAction("Leave","SchuifNext",self.CloseNext)
		new ZoneAction("Leave","SchuifPrev",self.ClosePrev)
	end
	
	void Init()
		CWriteLine("Init Schuifdeuren",180,0,255)
		tableclear(SchuifDeur.Lijst)
		self.Scan()
	end
	
	void NextRoom()
		ZoneAction.RoomChange( 1,"Start")
		self.Scan()
		self.Schuif("Prev",true,true)
	end
	
	void PrevRoom()
		ZoneAction.RoomChange(-1,"Einde")
		self.Scan()
		self.Schuif("Next",true,true)
	end
	
	void Schuif(string pn,bool open,instant)
		var dl
		var dr
		dl = SchuifDeur.Lijst[self.Kthura.Layer..pn.."Links"]
		dr = SchuifDeur.Lijst[self.Kthura.Layer..pn.."Rechts"]
		if dl
			dl.open = open
		end
		if dr
			dr.open = open
		end
		if instant
			if dl
				self.Kthura.Obj=dl.Name
				self.Kthura.ObjInt.x = dl.x-30
			end
			if dr
				self.Kthura.Obj=dr.Name
				self.Kthura.ObjInt.x = dr.x+30
			end
		end
	end
	
	void OpenNext()
		self.Scan()
		CSay("NEXT: Open Sesame!")
		self.Schuif("Next",true)
	end
	
	void CloseNext()
		CSay("NEXT: Sesam! Sluit u!")
		self.Schuif("Next",false)
	end
	
	void OpenPrev()
		self.Scan()
		self.Schuif("Prev",true)
	end
	
	void ClosePrev()
		self.Schuif("Prev",false)
	end
	
	void Cycle()
		// This is the callback function that runs on every game cycle 
		// to make sure the doors are operating properly when instructed 
		// to
		// CSay("Cycle!") // Must be on comment when nog debugging!
		for tag,deur in pairs(SchuifDeur.Lijst)
			if deur.Layer==self.Kthura.Layer
				self.Kthura.Obj=deur.Name
				switch deur.Richting
					case "Links"
						if deur.open and deur.x-31<self.Kthura.ObjInt.X
							CSay("NEXT: Open links!")
							self.Kthura.ObjInt.X--
						elseif (not deur.open) and deur.x>self.Kthura.ObjX(deur.Name)
							self.Kthura.ObjInt.X++
						end
					case "Rechts"
						if deur.open and deur.x+31>self.Kthura.ObjInt.X
							CSay("NEXT: Open rechts!")
							self.Kthura.ObjInt.X++
						elseif (not deur.open) and deur.x<self.Kthura.ObjX(deur.Name)
							self.Kthura.ObjInt.X--
						end
				end
			end
		end
	end
	
	void Scan()
		for t in each(self.scanlist)
			if self.Kthura.HasTag(t) and (not SchuifDeur.Lijst[self.Kthura.Layer..t])
				new SchuifDeur(self.Kthura.Layer,t)
				CSayF("Registering sliding door: %5s:%s",self.Kthura.Layer,t)
			end
		end
	end

end
