// License Information:
// src/Tricky Script/Script/Use/Specific/Combat.nlb/Combat_Heroes.nil
// Version: 19.09.19
// Copyright (C)  Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information









#use "libs/xmath"
#use "libs/strings"
#use "script/use/Anyway"
#use "script/use/BoxPics"



class Hero extends Fighter
	
	table Heroes
	int JumpIn = 90
	string Houding = "IDLE"
	int Frame = 0
	table Pics
	
	bool Leaving = false
	
	get int JI_MODX
		//int OUTX
		if self.JumpIn>0
			return (Screen.Width+70)-self.SpotX
		end
		return 0 
	end
	
	get int X
		if self.UseTemp
			return self.TempX
		end
		if self.JumpIn<=0
			return self.SpotX
		end
		return math.floor( self.SpotX + ((self.JI_MODX/90)*self.JumpIn) )
	end
	
	get int Y
		if self.UseTemp
			return self.TempY
		end
		if self.JumpIn<=0
			return self.SpotY
		end
		return math.floor( self.SpotY - (xmath.sin(self.JumpIn)*25) )
	end
	
	get int SpotNum
		for i=1,8
			if RPG.Party[i]==self.Tag
				return i
			end
		end
		error("Party member "..self.Tag.." not found! Cannot assign a number to them!")
	end
	
	string GetTeamName()
		return "Hero"
	end
	
	void AutoLoadPic()
		if self.Pics[self.Houding]
			return
		end
		string f
		f = sprintf("GFX/Combat/Fighters/Hero/%s.%s",self.Tag,self.Houding)
		if JCR_EntryExists(f..".png")
			f = f .. ".png"
		else
			f = f .. ".jpbf"
		end
		CSayF("Loading Image/Bundle: %s",f)
		self.Pics[self.Houding] = LoadImage(f)
	end
	
	void Draw()
		self.AutoLoadPic()
		// CSayF("Drawing %s.%s on (%d,%d)",self.Tag,self.Houding,self.X,self.Y) // <-- MUST BE ON COMMENT ALL THE TIME IF NOT IN USE!!!!
		__white
		SetAlpha(255)
		self.Pics[self.Houding].Draw(self.X,self.Y,self.Frame)
		if self.JumpIn>0
			self.JumpIn = self.JumpIn - 5
		end
	end
	
	void Init()
		var spot
		spot = ActPos.Spot[sprintf("HERO%d",self.SpotNum)]
		self.JumpIn = self.JumpIn + (#self.Heroes*45)
		self.SpotX = spot.X
		self.SpotY = spot.Y
	end
	
	static void Compile(string tag)
		CSayF("Compiling hero '%s' for combat!",tag)
		var myhero
		myhero = new Hero(tag)
		myhero.Heroes[#myhero.Heroes+1] = myhero
		myhero.Char = RPG.Char(tag)
	end
	
	static void CompileAll()
		for i=1,8
			if RPG.Party[i] and RPG.Party[i]!=""
				self.Compile(RPG.Party[i])
			end
		end
	end
end

class InputMenuItem
	
	static var List
	static var Hover
	static table Cursor
	
	int x
	int y
	delegate action
	delegate allowed
	
	void Clean()
		self.List.Clear()
	end
	
	void CONSTRUCTOR(int ax, int ay, delegate aaction, delegate aallowed)
		self.x = ax
		self.y = ay
		self.action = aaction
		self.allowed = aallowed
		self.List = self.List or new TLinkedList
		self.List.AddLast(self)
		End_Combat.Add(self.Clean)
	end
	
	
end

group Stage_SelectTarget extends Stage

	var Item
	var Ch
	string ItemTag
	string CharTag

	void CONSTRUCTOR()
		Stage.Stages.SELECTTARGET = self
	end
	
	void ComeToMe(string aCharTag,string aItemTag)
		self.CharTag = aCharTag
		self.ItemTag = aItemTag
		self.Item = _G.Item[aItemTag]
		self.Ch = RPG.Char(aCharTag)
		Stage.Go("SELECTTARGET")
	end
	
	void Draw()
	end
	
	void Update()
	end
end


group Stage_PlayerInput extends Stage

	var ImgMenu
	string CharTag
	
	bool AlwaysTrue()
		return true
	end
	
	bool AllowSwitch()
		return RPG.Party[5]!=nil and RPG.Party[5]!=""
	end
	
	void StartAbility()
	end
	
	void StartItem()
	end
	
	void StartAttack()
		Stage.Stages.SELECTTARGET.ComeToMe(self.CharTag,RPG.Char(self.CharTag).Data.Weapon)
	end
	
	void StartGuard()
	end
	
	void StartSwitch()
	end

	void CONSTRUCTOR()
		Stage.Stages.PLAYERINPUT=self
		self.ImgMenu = TImage.Obtain("COMBAT_INPUTMENU")
		
		// Abilities
		new InputMenuItem( 52,  0,self.StartAbility,self.AlwaysTrue)
		
		// Items
		new InputMenuItem(  0, 52,self.StartItem,self.AlwaysTrue)
		
		// Attack
		new InputMenuItem( 52, 52,self.StartAttack,self.AlwaysTrue)
		
		// Guard
		new InputMenuItem(104, 52,self.StartGuard,self.AlwaysTrue)
		
		// Switch
		new InputMenuItem( 52,104,self.StartSwitch,self.AllowSwitch)
		
		InputMenuItem.Cursor[true] = TImage.Obtain("COMBAT_INPUTCURSOR_true")
		InputMenuItem.Cursor[false] = TImage.Obtain("COMBAT_INPUTCURSOR_false")
		
	end
	
	void Draw()
		int mx
		int my
		mx =  Screen.Width/2
		my = (Screen.Height-128)/2
		self.ImgMenu.Draw(mx,(Screen.Height-128)/2)
		BoxPic((Screen.Width/2)-100,my,self.CharTag,nil,"top")
		Party.SelectedCharacterTag = self.CharTag
		InputMenuItem.Hover = nil;
		for item in InputMenuItem.List.Each
			if Mouse.X>item.x+mx and Mouse.Y>item.y+my and Mouse.X<item.x+52+mx and Mouse.Y<item.y+52+my
				InputMenuItem.Hover = item
				InputMenuItem.Cursor[item.allowed()].XDraw(mx+item.x,my+item.y)
			end
		end
	end
	
	void Update()
		if InputMenuItem.Hover and Mouse.HitLeft
			InputMenuItem.Hover.action()
		end
	end

end




