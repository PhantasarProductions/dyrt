// License Information:
// src/Tricky Script/Script/Flow/WorldMap.nil
// Version: 20.01.25
// Copyright (C)  Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information
#use "libs/tables"
#use "Script/Use/Anyway"


#macro WM_STARTY 25
#macro COLWIDTH 400


quickmeta class qm_new

	index
		return not gv.Bool["WorldMap_Visited_"..key]
	end
	
	newindex
		gv.Bool["WorldMap_Visited_"..key] = not value
	end
end

group F_WorldMap

	readonly table whole
	readonly var Font
	readonly table Unlocked
	readonly var Background
	readonly var NewImg
	readonly table NewSpot
	
	table filtered
	var Caption
	table Hover
		
	
	void CONSTRUCTOR()
		self.whole = NIL.Use("Script/Data/General/WorldMap.nil")
		self.Unlocked = WM_Unlocked
		self.Font = GetFont("BOXTEXT")
		self.Background = TImage.Obtain("MENUBACK")
		self.NewImg = LoadIfNew("gfx/WorldMap/new.png","IMG_NEW")
		self.NewSpot = new qm_new
	end
	
	void Filter(string continent)
		self.Caption = self.Font.Text(continent)
		self.filtered = {}
		int x = 5
		int y = WM_STARTY
		for k,v in spairs(self.whole)
			CSayF("WMFilter: Handing record: %s",k)
			bool show
			show = v.AvailableOnStart
			show = show or self.Unlocked[k]
			// This one below MUST be and, as it MUST override all
			// earlier settings when the continent is wrong!
			show = show and v.World==continent
			if show
				v.ID = k
				v.X = x
				v.Y = y
				v.GText = self.Font.Text(v.Name)
				if not v.MarkAsNew
					self.NewSpot[k] = false
				end
				#+ y 25
				if y > Screen.H - 200
					y = WM_STARTY
					#+ x COLWIDTH 
				end
				self.filtered[k]=v
			end
		end
	end
	
	void Draw()
		// Background! Always comes first
		__white
		self.Background.Tile(0,0,Screen.W,Screen.H)
		
		// Content
		__amber
		self.Hover = nil;
		self.Caption.Draw(Screen.W/2,4,2,0)
		for k,v in spairs(self.filtered)
			int ind = 0
			__white
			if self.NewSpot[k]
				self.NewImg.Draw(v.X,v.Y)
				#+ ind self.NewImg.Width+2
			end
			if Mouse.X>v.X and Mouse.Y>v.Y and Mouse.X<v.X+(COLWIDTH-20)  and Mouse.Y<v.Y+20
				self.Hover=v
				Color(180,180,255)
				#+ ind 10
			end
			v.GText.Draw(v.X+ind,v.Y)
		end
		
		// Party bar and stuff! Always comes last!
		__white
		Party.ShowBar()
		Achievements.DrawAll()
		__white
		Mouse.Show()
	end
	
	void Update()
		Dev.ConCheck()
		if Mouse.HitLeft and self.Hover
			Field.LoadMap(self.Hover.Kthura,self.Hover.Layer)
			Map.GoToLayer(self.Hover.Layer,self.Hover.Start)
			GoToFlow("FIELD")
			self.NewSpot[self.Hover.ID] = false
		end
	end
	
end


global delegate BUB_Draw
global delegate BUB_Update
BUB_Draw = F_WorldMap.Draw
BUB_Update = F_WorldMap.Update