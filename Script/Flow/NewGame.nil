// License Information:
// src/Tricky Script/Script/Flow/NewGame.nil
// Version: 19.08.03
// Copyright (C)  Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information








#use "script/use/anyway"
#use "libs/tables"
#use "libs/strings"

#macro next_marge_x 40
#macro next_marge_y 40

var Background
Background = TImage.Obtain("MENUBACK")

table stages
global int stage = 1
gvClearAll()

var font
var Font
font = LoadImageFont("Fonts/Coolvetica.20.jfbf")
Font = font

var bigfont
bigfont = LoadImageFont("Fonts/Coolvetica.35.jfbf")
var BigFont
BigFont=bigfont // Lazy, I know, but I don't care!

var bfont
bfont = LoadImageFont(FONT_Blitz)

var imgNext
imgNext = TImage.Obtain("NEXT")


#say "Translating stage: Game Jolt!"
class Stage_GameJolt
	
	readonly var gjlogo
	readonly table gjtext
	
	string gju_name
	string gju_token
	string gju
	table gju_capt
	table gjupos
	
	void CONSTRUCTOR()		
		self.gjlogo = LoadImage("GFX/alg/Game Jolt Transparent.png")
		self.gjtext = { font.Text("Here you can enter your Game Jolt creditials to allow"), font.Text("the game to log in onto Game Jolt and"), font.Text("log your achievements."), font.Text("There are also other benefits from using Game Jolt in the game"),font.Text("If you do not want that, you can just leave the fields"),font.Text("blank and move on into the next stage") }
		self.gju_capt = { ['name'] = "User name:", ['token'] = "Token:" }
		self.gjupos = { ['name'] = { ['pcx'] = 25 }, ['token'] = {['pcx']=50} }
		self.gju = 'name'
	end
	
	void check()
		bool allow
		allow = GameJolt.ByResource("Secu/Sites/GameJolt")
		CSayF("Allow Game Jolt outcome: %s",allow)
		if not allow
			stage = 2
		end
	end
	
	void draw()
		string scursor
		int y=100
		self.gjlogo.Draw(MidScreen.X-(self.gjlogo.Width/2),25)
		for tx in each(self.gjtext)
			tx.Draw(MidScreen.X,y,2,0)
			y = y + 25
		end
		for k,pos in pairs(self.gjupos)
			int x
			int w
			x = math.floor(Screen.Width*(pos.pcx/100))
			w = math.floor(Screen.Width*(22/100))
			if k==self.gju
				Color(255,255,0)
				scursor="_"
			else
				Color(255,255,255)
				scursor=""
			end
			bfont.DrawText(self.gju_capt[k],x,y)
			Rect(x,y+30,w,30)
			__black
			bfont.DrawText(self['gju_'..k]..scursor,x,y+31)
		end
		__white
		imgNext.Draw(Screen.Width-next_marge_x,Screen.Height-next_marge_y)
	end
	
	get bool LoginOk
		if self.gju_name=="" 
			gv.String.GameJolt_User  = ""
			gv.String.GameJolt_Token = ""
			gv.Boolean.GameJolt=false // Should not be needed, but I need to make sure no data from any possible earlier sessions lingers!
			return true
		end
		bool success
		repeat
			CSay("Trying to login on Game Jolt!")
			success = GameJolt.Login(self.gju_name,self.gju_token)
			if success
				gv.Boolean.GameJolt      = true
				gv.String.GameJolt_User  = self.gju_name
				gv.String.GameJolt_Token = self.gju_token
				CSay("= Success!")
				return true;
			end
			CSay("= Failed")
			string antwoord
			antwoord = FailureBox("Logging in did not work!\n- Did you enter the correct credentials?\n- Are you properly connected to the internet?\n- Are the Game Jolt servers up and running?\nOne of these is likely not in order!")
			switch antwoord
				case "Abort"
					CSay("= Login sequence aborted!")
					return false;
				case "Retry"
					CSay("= User requested to try it again!")
				case "Ignore"
					gv.String.GameJolt_User  = self.gju_name
					gv.String.GameJolt_Token = self.gju_token
					CSay("= User chose to ignore the error")
					return true;
				default
					error("Unknown output generated by dialog box")
			end
		forever
	end
	
	void update()
		string ch
		string nm
		int w
		w = math.floor(Screen.Width*(22/100)) // I know that this is not really efficient CPU wise, but please note that the overall code wasn't too demanding anyway, so if you notice the difference as a human being, your computer is too slow for modern software, anyway!
		ch = Keyboard.Char
		nm = Keyboard.Name:upper()
		// CSayF("Pressed %s > %s ",ch,nm) // debug!
		switch nm
			case "A" "B" "C" "D" "E" "F" "G" "H" "I" "J" "K" "L" "M" "N" "O" "P" "Q" "R" "S" "T" "U" "V" "W" "X" "Y" "Z" " " "SPACE" "OEMMINUS" "SUBTRACT" "D0" "D1" "D2" "D3" "D4" "D5" "D6" "D7" "D8" "D9" "NUMPAD0" "NUMPAD1" "NUMPAD2" "NUMPAD3" "NUMPAD4" "NUMPAD5" "NUMPAD6" "NUMPAD7" "NUMPAD8" "NUMPAD9" "OEM1" "OEM2" "OEM3" "OEM4" "OEM5" "OEM6" "OEM7" "OEM8" "OEM9" "OEM0"
				if bfont.TextWidth(self['gju_'..self.gju].."_x")<w
					self['gju_'..self.gju] = self['gju_'..self.gju] .. ch
				end
			case "BACK" "BACKSPACE"
				if self['gju_'..self.gju]!=""
					self['gju_'..self.gju] = Left(self['gju_'..self.gju],#(self['gju_'..self.gju])-1)
				end
			case "TAB"
				switch self.gju
					case "name"
						self.gju="token"
						return
					case "token"						
						self.gju="name"
						return
					default
						error(sprintf("?Huh? %s ?Huh?",self.gju))
				end
		end
		if Mouse.HeldLeft
			if Mouse.X>Screen.Width-next_marge_x and Mouse.Y>Screen.Height-next_marge_y
				if self.LoginOk
					stage = stage + 1
				end
			elseif Mouse.X<MidScreen.X
				self.gju="name"
			else
				self.gju="token"
			end
		end
	end
	
end

var stageGJ 
stageGJ = new Stage_GameJolt
stages[#stages+1] = stageGJ

#say "Difficulty settings!"
class Stage_Skill

	table skillnames
	table skilldesc
	table skillpic
	table skillnotes
	int hover = 0
	
	void CONSTRUCTOR()
		self.skillnames = { BigFont.Text("Stay away from me!!!"), BigFont.Text("Bring it on!"), BigFont.Text("There is no life in the VOID!") }
		self.skilldesc = { {Font.Text("In this mode the game is heavily simplefied"),Font.Text("More bonusses, quicker level gaining and more stuff will help you to get on the way"), Font.Text("This mode is intended for beginners!") }, {Font.Text("In this mode you'll get the game the way it was intended")}, {Font.Text("Think you are though?"),Font.Text("Can you beat Dr. Nim without cheating?"),Font.Text("Are you ready for the ultimate madness?"),Font.Text("If it's a hard game you want, a hard game you get!"),Font.Text("This mode is for the tough guys who need a life!") } }
		// The text here must be in REVERSED order!
		self.skillnotes = { 
			Font.Text("affected by this, as well. So therefore make your choice WISELY!"),
			Font.Text("Inventory space and level up tables and much more things, are"),
			Font.Text("This difficulty does not just affect enemy strength, you know!"),
			Font.Text("Please note that you can NOT change this later") 
		}
		for i=1,3			
			self.skillpic[i] = LoadImage("gfx/skill/"..i..".png")
			
		end
	end
	
	void draw()
		self.hover=0
		for i=1,3 
			__white
			self.skillpic[i]['Draw'](20,i*150)
			if Mouse.Y>i*150 and Mouse.Y<(i+1)*150
				self.hover=i
				for idx,txt in ipairs(self.skilldesc[i])
					txt.Draw(300,idx*40)
				end
				__skyblue
			end
			self.skillnames[i]["Draw"](100,(i*150)+80)
		end
		__white
		for i,t in ipairs(self.skillnotes)
			t.Draw(Screen.Width/2,Screen.Height-i*30,2,0)
		end
	end
	
	void update()
		if Mouse.HeldLeft
			if self.hover>0
				gv.Number.Skill=self.hover
				stage = stage + 1
			end
		end
	end
	
end
stages[#stages+1] = new Stage_Skill

#say "Language"
class NG_Lang
	string code
	string name
	var textname
	int x
	int y
	static int sx = 5
	static int sy = 50
	static var list
	static var hover
	
	void CONSTRUCTOR(string acode, string aname)
		self.list = self.list or new TLinkedList
		self.list.AddLast(self)
		self.code = acode
		self.name = aname
		self.textname = font.Text(aname)
		self.x = self.sx
		self.y = self.sy
		self.sy = self.sy + 25
		if self.sy>Screen.Width-100
			self.sy = 50
			self.sx = self.sx + 150
		end
		CSayF("Added language '%s' > '%s'", acode, aname)
	end
end

class Stage_Language
	void CONSTRUCTOR()
		for lcode, lname in spairs(Languages)
			new NG_Lang(lcode,lname)
		end
	end
	
	void draw()
		NG_Lang.hover=nil;
		for lang in NG_Lang.list.Each
			__white
			if Mouse.X>lang.x and Mouse.Y>lang.y and Mouse.X<lang.x+145 and Mouse.Y<lang.y+25
				lang.hover = lang
				__skyblue
			end
			lang.textname.Draw(lang.x,lang.y)
		end
	end
	
	void update()
		if Mouse.HitLeft and NG_Lang.hover
			__lang = NG_Lang.hover.code
			Names()
			stage = stage + 1
		end
	end
	
end


stages[#stages+1] = new Stage_Language


void StartGame()
	LoadFlow("FIELD","Script/Flow/Field.nil")
	RPG.Party.Max=8
	RPG.Party[1]="YoungIrravonia"
	Party.NewCharacter("YoungIrravonia",1)
	BoxText.Load("GENSCEN/CH1/STARTGAME","STARTGAME")
	Field.ChainMap("CH1_DUNG_Prison","Prologue_Start")
	//Field.LoadMap("CH1_DUNG_Prison","Prologue_Start") // Debug ONLY!
	GoToFlow("FIELD")
	KillFlow("NEWGAME")
end


class Stage_StartGame

	int CD = 25
	var LImage

	void CONSTRUCTOR()
		self.LImage = TImage.Obtain("LOADING")
	end
	
	void draw()
		// The countdown has to make sure the message actually appears on screen, as that is with this interface, hard to tell!
		self.CD = self.CD - 1
		self.LImage.Draw((Screen.Width/2)-(self.LImage.Width/2),(Screen.Height/2)-(self.LImage.Height/2))
	end
	
	void update()
		if self.CD<=0
			StartGame()
		end
	end
end

stages[#stages+1] = new Stage_StartGame






#say "To plus, or not to plus! That's the question!"
global void NewGamePlus()
	CSayF("NewGame+ from %s to %s",Done.NewGamePlus,Done.NewGamePlus)
end


#say "Checkup! Is Game Jolt Actually allowed?"
stageGJ.check()


#say "The actual stuff"
global void BUB_Draw()
	__white
	
	// debug
	Dev.ConCheck()
	
	// Autoupdate
	MidScreen.Auto()
	
	// Background
	Background.Tile(0,0,Screen.Width,Screen.Height)

	// Execute Stage Draw
	stages[stage]['draw']()
	
	// Mouse
	__white
	Mouse.Show()
end

global void BUB_Update()
	stages[stage]['update']()
end











