#use "libs/strings"
#use "script/use/anyway"


int cashlimit
cashlimit = 300000000 / __skill

#macro balancelimit 1000000000

var IMGUP
var IMGDOWN
var CIJFERS
var FONT
var CASH
var BALANCE
var BACK

IMGUP = TImage.Obtain("UP")
IMGDOWN = TImage.Obtain("DOWN")
CIJFERS = LoadImageFont("Fonts/ECN.jfbf")

FONT = GetFont("BIGTEXT")
CASH = FONT.Text("Shilders in cash:")
BALANCE = FONT.Text("Bank balance:")

BACK = TImage.Obtain("MENUBACK")


group Transfer
	string A
	bool hover = false
	int position
	int bpos
	
	get int bpv
		return math.ceil((10^(self.bpos))/10)
	end
	
	void D()
		if __bank+self.bpv>balancelimit
			// balancelimit is a macro only holding a numberic value, 
			// so doing this is safe as the computer will substitue the 
			// macro prior to actual compiling!
			Annoy("You cannot do this transaction! The max balance is balancelimit shilders")
			return
		end
		if (__cash>=self.bpv)
			#- __cash self.bpv
			#+ __bank self.bpv
		end
		CSayF("Transfer: %10d => %10d (deposit %d)",__cash,__bank,self.bpv)
	end
	
	void W()
		if __cash+self.bpv>cashlimit
			// Cash limit is a variable, and this this approach was needed!
			Annoy(sprintf("You cannot do this transaction! The max balance is %d",cashlimit))
		end
		if (__bank>=self.bpv)
			#+ __cash self.bpv
			#- __bank self.bpv
		end
		CSayF("Transfer: %10d <= %10d (withdraw %d)",__cash,__bank,self.bpv)
	end
	
end

global void PartyClick(int num)
	// GoMenu(num,"Field")
	GoToFlow("FIELD")
end



global void BUB_Draw()
	// start
	BACK.Tile(0,0,Screen.W,Screen.H)
	
	// do
	__amber
	CASH.Draw(10,10)
	BALANCE.Draw(10,200)

	string scash
	string sbank
	scash = tostring(__cash)
	sbank = tostring(__bank)
	
	
	Transfer.hover = false
	
	for i=1,#scash
		int j
		int x
		j = (1 + #scash)-i
		x = (Screen.W - 50) - (i*100)
		__white
		if (Mouse.X>x) and (Mouse.X<x+90) and Mouse.Y>40 and Mouse.Y<150
			Transfer.A = "D"
			Transfer.position = j
			Transfer.bpos = i
			Transfer.hover = true
			Color(160,180,255)
		end
		CIJFERS.DrawText(mid(scash,j,1),x,40)
	end

	for i=1,#sbank
		int j
		int x
		j = (1 + #sbank)-i
		x = (Screen.W - 50) - (i*100)
		__white
		if (Mouse.X>x) and (Mouse.X<x+90) and Mouse.Y>250 and Mouse.Y<360
			Transfer.A = "W"
			Transfer.position = j
			Transfer.bpos = i
			Transfer.hover = true
			Color(160,180,255)
		end
		CIJFERS.DrawText(mid(sbank,j,1),x,250)
	end

		
	// end
	Party.SelectedCharacter=0
	Party.ShowBar()
	Achievements.DrawAll()
	__white
	Mouse.Show()
end

global void BUB_Update()
	Dev.ConCheck()
	Achievements.Auto()
	Party.Click()
	
	if Mouse.HitLeft and Transfer.hover
		Transfer[Transfer.A]()
	end
end
