// License:
// **********************************************
// 
// src/Script/Script/Flow/Mini/RedDragonPuz.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.04.28 V
// 
// End License

Script

#use "Libs/SCI/Graphics"

#use "script/use/need"
#use "Script/Use/Misc/StartCombatMod"
//#use "libs/tables"

var IMGUP
var IMGDOWN
var CIJFERS

Init
	IMGUP = TImage.Obtain("UP")
	IMGDOWN = TImage.Obtain("DOWN")
	CIJFERS = LinkFont("Fonts/ECN.jfbf")
End
		

class Kleur
	readonly int R
	readonly int G
	readonly int B
	static readonly int upy = 64
	static readonly int dny = 134
	static readonly int szy = 32
	
	int X
	int Y
	
	get int x
		return X
	end
	
	get int y
		return Y
	end
	
	readonly int Goed
	
	int t_waarde
	
	get int waarde
		return self.t_waarde
	end
	
	set int waarde
		t_waarde = math.abs((value + 10) % 10)
	end
	
	CONSTRUCTOR(int rood,int groen,int blauw,int goed)
		self.R = rood
		self.G = groen
		self.B = blauw
		self.Goed = goed
	end
	
	void ZetKleur()
		Color($R,$G,$B)
	end
	
	void DrukAf(int _x,int _y)
		self.ZetKleur()
		CIJFERS.DrawText(tostring(self.waarde),_x,_y,2)
		__white
		IMGUP.Draw(_x-16,_y-self.upy)
		IMGDOWN.Draw(_x-16,_y+self.dny)
		self.X = _x
		self.Y = _y
		
	end
		
	get bool Up
		return Mouse.X>x-16 && Mouse.X<x+16 && Mouse.Y>y-upy && Mouse.Y<(y-upy)+32 && Mouse.HitLeft
	end

	get bool Down
		return Mouse.X>x-16 && Mouse.X<x+16 && Mouse.Y>y+dny && Mouse.Y< y+dny+32  && Mouse.HitLeft
	end
	
end

var Rood
var Wit
var Blauw
Init
	Rood  = new Kleur(255,  0,  0,2)
	Wit   = new Kleur(255,255,255,1)
	Blauw = new Kleur(  0,  0,255,9)
End

int colw = math.floor(Graphics.W/4)

table KleurSet = { [0]=Rood, Wit, Blauw }

Global Void MainFlow()
//global void BUB_Draw()
	// The three digits
	for i,k in pairs(KleurSet)
		k.DrukAf((i+1)*colw,100)
	end
	
	// Party bar (no further function of that)
	Party.SelectedCharacter=0
	Party.ShowBar()
	
	// Draw all achievement boxes
	Achievements.DrawAll()
	
	// Show Mouse
	__white
	Mouse.Show()
//end


//global void BUB_Update()
	Dev.ConCheck()
	
	if Mouse.HitRight
		Map.WipeSASKIA()
		GoToFlow("FIELD")
	end
	
	bool goed = true
	
	for k in each(KleurSet)
		if k.Up
			++ k.waarde
		elseif k.Down
			-- k.waarde
		end
		goed = goed && k.waarde == k.Goed
	end
	
	if goed
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/Special.wav"
		StartCombat.Arena = "RedDragonCave"
		StartCombat.Foe[1] = "Boss/RedGuardian"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
end
