// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH4_DUNG_BlackPrison.nil
// (c) Jeroen Broks, 2020, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 20.05.22
// End License Information



#say "Map Script - None!"
#use "script/use/anyway"
#use "Script/Use/StartCombatMod"
// #use "Libs/Collections"

#macro Voltooid gv.Bool.BlackPrison_Merya_Solo_Rescue_Eric_Completed

#macro MaxLayers 6
#macro MaxGuards 25

bool EntranceChecked = false
bool MeryaOnlyChecked = false

class TGuardZone
	readonly int SX
	readonly int SY
	readonly int EX
	readonly int EY
	
	void CONSTRUCTOR(int ASX,int ASY,int AEX,int AEY)
		self.SX = ASX
		self.SY = ASY
		self.EX = AEX
		self.EY = AEY
		CSayF("Created GuardZone (%d,%d)-(%d,%d)",self.SX,self.SY,self.EX,self.EY)
	end
	
	void DESTRUCTOR()
		CSayF("Destroyed GuardZone (%d,%d)-(%d,%d)",self.SX,self.SY,self.EX,self.EY)
	end
end

class TGuard
	readonly table Zones
	readonly string Lay
	readonly string Tag
	readonly var Z1
	readonly var Z2
	
	void CONSTRUCTOR(string l,string g)
		self.Zones = setmetatable({},{
			["__index"] = var(s,i)
				return self['Z'..i]
			end
		})
		CSayF("Creating guard '%s' on '%s'!",g,l)
		self.Lay = l
		self.Tag = g
		Map.Layer = l
		Map.Obj = g
		int x
		int y
		string w
		x = Map.ObjInt.X
		y = Map.ObjInt.y
		w = Map.ObjString.Texture:upper()
		w = replace(w,'GFX/ACTORS/MERYAGUARD/','')
		w = replace(w,'.PNG','')
		self.Z1 = new TGuardZone(x-32,y-32,x+32,y+32)
		switch w 
			case "NORTH"
				self.Z2 = new TGuardZone(x    ,y-160,x    ,y+ 32)
			case "SOUTH"
				self.Z2 = new TGuardZone(x    ,y    ,x    ,y+160)
			case "WEST"
				self.Z2 = new TGuardZone(x-160,y    ,x    ,y    )
			case "EAST"
				self.Z2 = new TGuardZone(x    ,y    ,x+160,y    )
			default
				error("Unknown guard wind: "..w)
		end
	end
	
	void DESTRUCTOR()
		CSayF("Destroying guard '%s' on '%s'!",self.Tag,self.Lay)
	end
	
	get bool Check
		bool ret = false
		if Map.Layer!=self.Lay
			return false
		end
		for i=1,2
			ret = ret or (Map.PlayerX>=self.Zones[i].SX and Map.PlayerY>=self.Zones[i].SY and Map.PlayerX<=self.Zones[i].EX and Map.PlayerY<=self.Zones[i].EY)
		end
		return ret
	end
end


group MScript
	
	table Guard
	readonly var GunShot

	
	void LEAVE()
		if Voltooid
			WorldMap.Delisto()
			return
		end
		if Yes("Do you want to leave the prison for now?")
			for i,c in ipairs { "Kirana", "Irravonia", "Brendor", "Scyndi", "Rebecca", "Dernor", "Merya", "Aziella" }
				RPG.Party[i] = c
			end
			WorldMap.Delisto()
		end
	end
	
	void MERYAONLY()
		if Voltooid or MeryaOnlyChecked
			return
		end
		MeryaOnlyChecked = true
		for i=2,8
			RPG.Party[i]=""
		end
		RPG.Party[1] = "Merya"
		if not Done.BlackPrison_Merya_Solo_Introduction_Shown
			MapText("ENTER")
		end
	end
	
	void CHECKBARRIER()
		if EntranceChecked
			return
		end
		if not Voltooid
			Map.Kill("Barrier")
		end
		EntranceChecked=true
	end
	
	void TUTSNEAK()
		if not Done.BlackPrison_Merya_Solo_Tutorial_Sneaking
			MapText("SNEAKTUT")
		end
	end
	
	void InitGuards()
		if Voltooid
			return
		end
		CSay("Init Guards")
		self.Guard = {}
		for layi = 2,MaxLayers
			string layname			
			layname = sprintf("#%03d",layi)
			self.Guard[layname] = {}
			CSayF("Checking for guards on floor '%s",layname)
			Map.Layer = layname
			for gi = 1,MaxGuards
				string guardtag
				CSayF("Found guard #%d",gi)
				guardtag = sprintf("GUARD%d",gi)
				if Map.HasTag(guardtag)
					self.Guard[layname][guardtag] = new TGuard(layname,guardtag)
				end
			end
		end
	end
	
	void CheckGuards()
		for tag,data in pairs(self.Guard[Map.Layer])
			if data.Check
				CSayF("%s sees Merya and kills her!",tag)
				Map.StopWalking()
				Map.Obj="Player"
				Map.ObjString.Texture="GFX/combat/Fighters/Hero/Merya.Dead.png"
				self.GunShot.Play()
				MapScript.SASKIA("MeryaIsDead")
			end
		end
	end
	
	void SWITCH_ERICBARRIER()
		Map.Kill("EricBarrier",true)
	end
	
	void ENTERERIC()
		Map.ShowByLabel("Eric")
	end
	
	void MORTWELCOME()
		if not Done.BlackPrison_Mort_Welcome
			Party.LvUp("Merya")
			MapText("MORTWELCOME")
		end
		if Voltooid
			MapScript.SASKIA("Dzgjymza")
		end
	end
	
	void FIGHTMORT()
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/Special.wav"
		StartCombat.Arena = "BlackPrison"
		StartCombat.Foe[1] = "Boss/Mort"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
	
	void NPC_ERIC()
		Voltooid = true
		Achievements.Award("SCEN_BLACKPRISON")
		RPG.Party[2] = "Eric"
		MapScript.SASKIA("Eric")
	end
	
	void CONSTRUCTOR()
		if not Voltooid
			Map.ZA_Enter("MeryaOnly","MeryaOnly")
			Map.ZA_Enter("CheckBarrier","CheckBarrier")
			self.InitGuards()
		end
		Map.ZA_Enter("Leave","Leave")
		Map.ZA_Enter("TutSneak","TutSneak")
		self.GunShot = LoadAudio("SFX/Black Prison/GunShot.Wav")
		Map.ZA_Enter("EnterEric","EnterEric")
		Map.ZA_Enter("MortWelcome","MortWelcome")
	end
	
	void Draw()
	end
	
	void Update()
		if (not Voltooid) and Map.Layer!="#000" and Map.Layer!="#001" and Map.Layer!="#007"
			self.CheckGuards()
		end
	end
	
end
#say "Map Script - Compiling!"
