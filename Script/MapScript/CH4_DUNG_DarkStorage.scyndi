// License:
// **********************************************
// 
// src/Script/Script/MapScript/CH4_DUNG_DarkStorage.scyndi
// (c) Jeroen Broks, 2020, 2021, 2022, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.06.24
// 
// End License




Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Kthura"
#use "Libs/SCI/InterVar"

#use "script/use/Need"
#use "Script/Use/Misc/StartCombatMod"

#macro OnyxBlock gv.Bool.DarkStorage_OnyxBlock
#macro OnyxKilled gv.Bool.DarkStorage_OnyxKilled
#macro SoulOrbsSeen gv.Bool.DarkStorage_Soul_Orbs_Seen

bool MDL_checked

group MScript

	bool medal_checked = false
	
	void GOSUB(int i, string onyx)
		Field.LoadMap("CH4_DUNG_DarkStorage_"..onyx)
		Map.GoToLayer("#"..i.."00","Start")
	end
	
	void MEANWHILE()
		if Inventory.KEY_DARKSTORAGEKEY>=4 && (! Done.DarkStorageOnyx_Meanwhile_Cutscene)
			MapScript.SASKIA("Meanwhile")
		elif gbool.DarkStorageOnyx_Meanwhile_Cutscene
			Map.Kill("Deur")
		end
	end
	
	void MDL_OPEN()
		LocExtern WantRatio "64"
		CSayF("Medal ratio: %05.1f%%; must be at least:%03d%%",MedalRatio,WantRatio)
		if MedalRatio>=WantRatio
			Map.Kill("Medal_Barrier",false)
			Kthura.ShowByLabel("Medal")
			Kthura.ShowByLabel("GJ") // Should be obsolete, but I cannot be sure.
			CSayF("-> Open Sesame!")
		End
		medal_checked=true
	end
	
	void FROM_MEDAL()
		if medal_checked
			return
		end
		Kthura.ShowByLabel("Medal")
		Kthura.ShowByLabel("GJ") // Should be obsolete, but I cannot be sure.
		Map.Kill("Medal_Barrier")
		medal_checked = true	
	end
	
	void ENTERMORKER()
		Field.LoadMap("MDL_DUNG_MORKER")
		Map.GoToLayer("#001","Start")
	end
	
	void ALTPREV()
		if OnyxBlock
			Map.StopWalking()
			Map.WalkTo("Start")
			MapText("NOTBACK_"..Upper(Map.Leader))
		else
			Map.GoToLayer("#002","Einde")
		end
	end
	
	void BLOCKONYX(bool v)
		OnyxBlock = v
		CSayF("Onyx block: %s",tostring(v))
	end
	
	void ONYXSTART()
		if OnyxKilled 
			return
		end
		MapScript.SASKIA("Onyx")
	end
	
	void ONYX()
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/OrderOfOnyx.wav"
		StartCombat.Arena = "DS_Main"
		StartCombat.Foe[1] = "Boss/Aldarus3"
		StartCombat.Foe[2] = "Boss/Kirana3"
		StartCombat.Foe[3] = "Boss/Rondomo3"
		StartCombat.Foe[4] = "Boss/Jeracko3"
		
		StartCombat.RX[1] = 25
		StartCombat.RY[1] = 60
		
		StartCombat.RX[2] = 25
		StartCombat.RY[2] = 75
		
		StartCombat.RX[3] = 40
		StartCombat.RY[3] = 60
		
		StartCombat.RX[4] = 40
		StartCombat.RY[4] = 75
		
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
	
	void ONYXISKILLED()
		OnyxKilled = true
		CSay("Onyx is killed")
	end
	
	void NPC_KIRANA()
		if not SoulOrbsSeen
			return
		end
		MapScript.SASKIA("Kirana")
	end
	
	void REMOVEERIC()
		if Done.DarkStorate_RemoveEric
			// Technically this should be unneeded, as Eric will come 
			// back as "Yasathar", but to be sure no bugs can happen
			return
		end
		bool Eric = false
		for i=1,7
			Eric = Eric || RPG.Party[i]=="Eric"
			if Eric
				RPG.Party[i] = RPG.Party[i+1]
			end
		end
		RPG.Party[8]=""
	end
	
	void NPC_SOUL()
		if Done.DarkStorage_Soul_Orbs_Seen
			MapText("SIGN")
			return
		end
		MapScript.SASKIA("Souls")
	end
	
	void ADDKIRANA()
		var Kirana
		Party.NewCharacter("Kirana",300/__skill)
		Kirana = RPG.GetChar("Kirana")
		for i=8,2,-1
			RPG.Party[i] = RPG.Party[i-1]
		end
		RPG.Party[1] = "Kirana"
		skn:={[0]="Scythe slashing","Ignis","Aer","Aqua","Terra"}
		for i=0,4
			Kirana.Points("SKILLLVL"..i).Have = Kirana.Points("SKILLLVL"..i).Maximum
			Kirana.Data[sprintf("SKILL%d",i)]=skn[i];
		end
		for _,e in pairs { "Flame","Wind","Water","Earth","Frost","Thunder" }
			Kirana.Stat("BASE__RESIST_Elem_"..e).Value=math.random(60/__skill,90/__skill)
		end
		Kirana.Stat("BASE__RESIST_Elem_Darkness").Value=math.random(90/__skill,96/__skill)
		Kirana.Stat("BASE__RESIST_Elem_Light").Value=-math.random(10*__skill,30*__skill)
		OnyxBlock=false	
	end

	void Draw()
	end
	
	void Update()
	end
	
end



init
	for i,onyx in pairs { "Aldarus", "Jeracko", "Kirana", "Rondomo" } 
		Map.ZA_Enter("Go"..onyx,"GoSub",i,onyx)
	end
	Map.ZA_Enter("MainHall","Meanwhile")
	Map.ZA_Enter("Check_Medal","MDL_Open")
	Map.ZA_Enter("From_Medal","FROM_Medal")
	Map.ZA_Enter("ToMorker","EnterMorker")
	Map.ZA_Enter("AltPrev","AltPrev")
	Map.ZA_Enter("Onyx","OnyxStart")
	Map.ZA_Enter("RemoveEric","RemoveEric")
end
