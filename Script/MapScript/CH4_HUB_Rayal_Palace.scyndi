// License:
// **********************************************
// 
// src/Script/Script/MapScript/CH4_HUB_Rayal_Palace.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.05.22 XIII
// 
// End License




Module

#use "Libs/SCI/Sys"
#use "Libs/SCI/Kthura"
#use "Libs/SCI/InterVar"

#use "script/use/Need"
#use "Script/Use/Misc/StartCombatMod"
//#use "libs/tables"

#macro ScharumMusic "Music/Combat/Random/Chapter4.wav"

group TempParty
	table pushed
	void Push()
		string L
		L = Map.Leader
		for i=1,8 
			self.pushed[i] = RPG.Party[i]
			if i==1 
				RPG.Party[1] = L
			else
				RPG.Party[i] = ""
			end
		end
	end
	void Pop()
		for i=1,8 
			RPG.Party[i] = self.pushed[i]
		end
	end
end

group MScript
	
	table HideZones
	bool dominate
	
	void HideSubs()
		CSay("Hide Subs: Fake!")
		Kthura.CurrentLayer="PALACE"
		//Map.VisibleOnlyByZone("Fake",false)
		for z in each(self.HideZones)
			CSayF("Hide Subs Zone: %s",z)
			Map.HideByZone(z)
		end
	end
	
	//link HIDESUBS = MScript.HideSubs
	
	void SHOWPART(string z)
		//Map.VisibleOnlyByZone(z,true)
		Map.ShowByZone(z)
		Map.HideByLabel("H")
		Map.HideObj("Katha")
	end
	
	void SCHARUMSTART()
		TempParty.Push()
		StartCombat.StartSession()
		StartCombat.Music = ScharumMusic
		StartCombat.Arena = "ExamRuins"
		StartCombat.Foe[1] = "Boss/Scharum"
		StartCombat.NoLose = true
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
	
	void SCHARUMEND()
		TempParty.Pop()
		if gv.Bool.Victor
			if ! Done["Scharum_"..Map.Leader]
				CSayF("Unlocking Scharum Ability for %s",Map.Leader)
			end
			MapText("SCHARUM.WIN")
		else
			MapText("SCHARUM.LOSE")
			RPG.GetChar(Map.Leader).Points("HP").Have = 1
		end
	end
	
	void NPC_SCHARUM()
		MapScript.SASKIA("Scharum")
	end
	
	void NPC_KING()
		if ! Done.FirstTimeHerondoRayal
			Map.HideObj("Player")
			Map.ShowByLabel("H")
			MapScript.SASKIA("Herondo_FirstTime")
			return
		end
		if Map.Leader=="Aziella"
			MapScript.SASKIA("Herondo_Aziella")
		else
			MapText("KONINGALG")
		end
	end
	
	void NIGHTNIGHT()
		for i=2,8
			RPG.Party[i]=""
		end
		RPG.Party[1] = "Eric"
		Map.Leader = "Eric"
		//Field.LoadMap("CH4_HUB_Rayal_Palace_Upstairs_Night")		
		//Map.GoToLayer("NACHT","Start_Nacht")
		//MapScript.ShowPart("Slaapkamer")
		//MapText("NOSLEEP")
		Field.ChainMap("CH4_HUB_Rayal_Palace_Upstairs_Night","Arrive_Eric","NACHT")
		Map.GoToLayer("NACHT","Start_Nacht_Neutral")
	end
	
	void Guard()
		MapText("GUARD")
	end
	
	link NPC_GUARD1 = MScript.Guard
	link NPC_GUARD2 = MScript.Guard
	
	void STARTERICMISSION()
		for i=4,8
			RPG.Party[i]=""
		end
		RPG.Party[1] = "Eric"
		RPG.Party[2] = "Irravonia"
		RPG.Party[3] = "Brendor"
		RPG.Party[4] = "Aziella"
		Map.Kill("StartSplit")
	end
	
	void LEAVE()
		if gv.Bool.FirstTimeHerondoRayal
			WorldMap.Aeria()
		else
			Map.WalkTo("Start")
			MapText("KINGFIRST")
		end
	end
	
	void YASATHAR_DRESS()
		Map.SpawnPlayer("Split_Start")
		//Map.Obj="Eric"
		//Map.ObjString.Texture="GFX/actors/heroes/Yasathar.North.jpbf/00000000.png"
		Kthura.Obj.Eric.Texture="GFX/actors/heroes/Yasathar.North.jpbf/00000000.png"
		Map.HideObj("Player")
		Map.ShowByLabel("H")
		self.SHOWPART("Kings_Room")
	end
	
	
	
	void Draw()
	end
	
	void Update()
		if ! $dominate
			//Map.Obj="Player"
			Kthura.Obj.Player.Dominance=40
			$dominate=true
		end
	end
	
end


Init
	CSay("Hide Subs::Init")
		
		MScript.HideZones = { [0]="Scharum", "Kings_Room","Garden", "SecretPassage" }
	CSay("Hide Subs::Run")	
		MScript.HideSubs()
	CSay("Hide Subs::ZA")
		for z in each(MScript.HideZones)
			CSayF("Subs ZA: %s",z)
			Map.ZA_Enter(z,"SHOWPART",z)
			Map.ZA_Leave(z,"HideSubs")
		end
	CSay("Oracle and split mission check")	
		if gv.Bool.SpokenToOracle && (!Done.SplitMissionStart_Eric)
			Map.ZA_Enter("StartSplit","StartEricMission")
		end
	CSay("Aziella checkup")
		var Az
		Az = RPG.GetChar("Aziella")
		if Az.Points("SKILLLVL0").Have == 1
			Az.Points("SKILLLVL0").Have = 24 / __skill
		end
		if Az.Points("SKILLLVL1").Have == 0
			Az.Points("SKILLLVL1").Have = 24 / __skill
		end
	CSay("Zone Action")	
		Map.ZA_Enter("Leave","Leave")
	CSay("Welcome to Rayal Palace")
end
