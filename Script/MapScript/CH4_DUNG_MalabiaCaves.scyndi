// License:
// **********************************************
// 
// src/Script/Script/MapScript/CH4_DUNG_MalabiaCaves.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.05.24 XVII
// 
// End License


Script

#use "script/use/need"


#use "libs/linkedlist"

#use "libs/sci/sys"
#use "libs/sci/kthura"
#use "libs/sci/intervar"

class Knopje

	static table All
	static table Room
	static int TimeOut = 15000
	
	readonly var Nodes
	readonly string tag
	readonly string roomtag
	
	DESTRUCTOR
		Nodes.Clear()
		table kill
		CSay("Destroying Knopje: "..tag)
		All[tag] = nil;
		Room[tag] = nil;		
	end
	
	bool HasNode(string n)
		for N in self.Nodes.Each
			if N.tag==n
				return true
			end
		end
		return false
	end
	
	void Add(string Node,string Room,dontcomeback)
		--TimeOut
		assert(TimeOut>0,"Addition Time-Out!")
		//if not self.HasNode(Node)
		CSay("Check self.All[\""..Node.."\"] >> "..Lua.tostring(self.All[Node]))
		if ! self.All[Node]
			// Wrong!!
			// self.Nodes[Node] = self.Nodes[Node] or new Knopje(Node,Room)
			// self.Nodes[Node].Add(self.tag,Room)
			CSay("= Creating Node: "..Node..", in order to link to "..self.tag)
			var NN
			NN = new Knopje(Node,Room)
			self.Nodes.AddLast(NN)
			//if dontcomeback!=false
			//	NN.Add(self.tag,Room,true)
			//end
		else
			CSay("= Adding existing node: "..Node)
			self.Nodes.AddLast(self.All[Node])
		end
		
	end
	
	get bool On
		//Map.Obj = self.tag
		r:= upper(Kthura.Obj[tag].Texture) == "GFX/TEXTURES/JEWELBUTTONS/GREEN.PNG"
		return r
	end
	
	set bool On
		if value
			Kthura.Obj[tag].Texture = "GFX/TEXTURES/JEWELBUTTONS/GREEN.PNG"
		else
			Kthura.Obj[tag].Texture = "GFX/TEXTURES/JEWELBUTTONS/BLUE.PNG"
		end
	end
	
	void Toggle()
		self.On = ! self.On
	end
	
	void FullToggle()
		self.Toggle()
		for Node in self.Nodes.Each
			Node.Toggle()
		end
	end
	
	void LinkTo(string Room,infinity)
		for i,Node in Lua.ipairs{infinity}
			assert(type(Node)=="string","All nodes must be strings!")
			self.Add(Node,Room)
		end
	end
	
	static var MK(string _tag, string Room, infinity)
		self.All[_tag] = self.All[_tag] || new Knopje(_tag,Room,infinity)
		self.All[_tag].LinkTo(Room,infinity)
		return self.All[_tag]
	end
	
	CONSTRUCTOR(string _tag, string _Room, infinity)
		assert(! self.All[_tag],"Duplicate switch button: ".._tag.."/".._Room)
		Map.ZA_Enter(_tag,"KNOPJE",_tag)
		CSayF("Creating %s::%s",_Room,tag)
		self.Nodes = new TLinkedList()
		self.Room[_Room] = self.Room[_Room] || {}
		self.Room[_Room] += self
		self.All[_tag] = self
		self.tag = _tag
		self.roomtag = _Room
	end
	
end

quickmeta Solved
	index
		return gv.Bool["MALABIACAVES_KNOPJE_"..key]
	end
	
	newindex
		gv.Bool["MALABIACAVES_KNOPJE_"..key] = value
	end
end


group MScript
	
	void TUT_SCHIETEN()
		if ! Done.Tutorial_Aziella_Kan_Schieten
			MapText("TUT_SCHIETEN")
		end
	end
	
	void SCHIET_KAMER1()
		Map.Kill("Barrier",true)
		Achievements.Award("PUZ_180")
	end
	
	void SCHIET_MAAKBRUG()
		string m
		//string doit = "Map.Obj='Brug';\tMap.ObjBool.Visible=true;\tMap.ObjBool.ForcePassible=true"
		//delegate doitf		
		//doitf = Lua.assert(Lua.load(doit,"Maak een brug"))
		//doitf()
		//m = "MAPCHANGES.CH4_DUNG_MalabiaCaves"
		//Swap.Data[m] = Swap.Data[m] .. "FieldMap.Kthura.Layer=\""..Map.Layer.."\";"..doit.."\n"
		Kthura.obj.Brug.Visible=true
		Kthura.obj.Brug.ForcePassible=true		
		gbool["MalabiaCaves_"..Kthura.CurrentLayer]=true
	end
	
	void KNOPJE(string tag)
		var K
		K = Knopje.All[tag]
		if Solved[K.roomtag]
			return
		end
		K.FullToggle()
		bool S = true
		for _,k in ipairs(Knopje.Room[K.roomtag])
			S = S and k.On
		end
		Solved[K.roomtag] = S
		if S
			Map.Kill("Barrier",true)
			for _,k in ipairs(Knopje.Room[K.roomtag])
				Map.Kill(k.tag,true)
			end
			if Map.Layer=="#008"
				Achievements.Award("PUZ_INSANE")
			end
		end
	end
	
	void BOVEN()
//		Map.Obj="Boven"
//		Map.ObjBool.ForcePassible=true
//		Map.Obj="Beneden"
//		Map.ObjBool.ForcePassible=false
//		Map.Obj="Player"
//		Map.ObjInt.Dominance = 50
		Kthura.Obj.Boven.ForcePassible=True
		Kthura.Obj.Beneden.ForcePassible=False
		Kthura.Obj.Player.Dominance = 50
		CSay("Upstairs")
		Kthura.Remap()
	end

	void BENEDEN()
		//Map.Obj="Boven"
		//Map.ObjBool.ForcePassible=false
		//Map.Obj="Beneden"
		//Map.ObjBool.ForcePassible=true
		//Map.Obj="Player"
		//Map.ObjInt.Dominance = 20
		Kthura.Obj.Boven.ForcePassible=False
		Kthura.Obj.Beneden.ForcePassible=True
		Kthura.Obj.Player.Dominance = 20
		CSay("Downstairs")
		Kthura.Remap()
	end
	
	void TOTHETEMPLE()
		WM_Unlocked.A_MALABIATEMPLE = true
		WorldMap.Aeria()
	end
	
	
		
	void Draw()
	end
	
	void Update()
	end
	
end
	
Init
		Map.ZA_Enter("TUT_Schieten","TUT_Schieten")
		Map.ZA_Enter("Oben","Boven")
		Map.ZA_Enter("Upstairs","Boven")
		Map.ZA_Enter("Unten","Beneden")
		Map.ZA_Enter("Downstairs","Beneden")
		Map.ZA_Enter("NaarDeTempel","ToTheTemple")
		// #002
		Knopje.MK("One",   "#002","Two")
		Knopje.MK("Two",   "#002","Three","One")
		Knopje.MK("Three", "#002","Four","Two")
		Knopje.MK("Four",  "#002","Five","Three")
		Knopje.MK("Five",  "#002","Four")
		// #004
		Knopje.MK("Een",   "#004","Acht", "Twee")
		Knopje.MK("Twee",  "#004","Een",  "Drie")
		Knopje.MK("Drie",  "#004","Twee" ,"Vier")
		Knopje.MK("Vier",  "#004","Drie" ,"Vijf")
		Knopje.MK("Vijf",  "#004","Vier", "Zes")
		Knopje.MK("Zes",   "#004","Vijf", "Zeven")
		Knopje.MK("Zeven", "#004","Zes",  "Acht")
		Knopje.MK("Acht",  "#004","Zeven","Een")
		// #008
		for i=1,3
			for j=1,3
				var kn
				kn = Knopje.MK(lstring.char(i+64)..lstring.char(j+48),"#008")
				if i>1
					kn.Add(lstring.char(i+63)..lstring.char(j+48),"#008")
				end
				if i<3
					kn.Add(lstring.char(i+65)..lstring.char(j+48),"#008")
				end
				if j>1
					kn.Add(lstring.char(i+64)..lstring.char(j+47),"#008")
				end
				if j<3
					kn.Add(lstring.char(i+64)..lstring.char(j+49),"#008")
				end
			end
		end
		oldb:=Kthura.CurrentLayer
		for i=0,8
			tb:=sprintf("#%03d",i)
			if gbool["MalabiaCaves_"..tb]==true
				Kthura.CurrentLayer=sprintf("#%03d",i)
				Kthura.obj.Brug.Visible=true
				Kthura.obj.Brug.ForcePassible=true
			end
		end
		if oldb!="";Kthura.CurrentLayer=oldb;end
End

#say "Map Script - Compiling!"
