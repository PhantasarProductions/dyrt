// License:
// **********************************************
// 
// src/Script/Script/MapScript/CH4_DUNG_Frundarmon.scyndi
// (c) Jeroen Broks, 2020, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.05.29
// 
// End License


Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/Events"
#use "Libs/SCI/SString"
#use "Libs/SCI/InterVar"
#use "Libs/SCI/Graphics"

#use "script/use/Need"
#use "Script/Use/Misc/StartCombatMod"
#use "Script/Use/MapScript/CircularDarkness"
#use "Script/Use/MapScript/EndSplit"


//#use "libs/LinkedList"

#macro ClosetPuzzle gv.Bool.Frundarmon_Closet_Puzzle_Solved
#macro FrunDefeated gv.Bool.DefeatedFrundarmon



table KidGen = { ["Befindo"] = "boy",  ["human"]   = "boy", ["Fairy"]   = "girl", ["Phelynx"] = "girl", ["Elf"]     = "girl" }


group MScript
	
	link THEEND = EndSplit.RunEndSplit
	
	bool kidkill_diningroom
	
	//var Lijst
	string Kamer
	var Font
	
	// table boy
	// table girl
	
	table KamerKop
	
	void ENTERKAMER(string tag)
		Map.VisibleOnlyByZone(tag,true)
		self.Kamer = replace(tag,"_"," ")
		self.KamerKop[tag] = self.Font.iText(tag)
	end
	
	void InitKamer(string tag)
		//$Lijst.AddLast(tag)
		Map.ZA_Enter(tag,"EnterKamer",tag)
		CSayF("= Initiated Kamer: %s",tag)
	end
	
	void KID(infinity)
		StartCombat.StartSession()
		StartCombat.Music = "*NONE*"
		StartCombat.Arena = "Frundarmon"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		for i,kid in Lua.ipairs {infinity}
			StartCombat.Foe[i] = sprintf("Kids/%s",kid)
		end
		StartCombat.Ready()
	end
	//link Kid = MScript.KID
	
	void SWITCH_TO2()
		Map.Kill("Barrier_TO2",true)
	end
	
	void TOBASEMENT()
		Field.LoadMap("CH4_DUNG_FrundarmonBasement")
		Map.GoToLayer("#000","Start")
	end
	
	void WELCOME2()
		if ! Done.Frundarmon_Welcome_2ndFloor
			self.Kid("Elf","Human","Phelynx","Fairy")
		end
		if (! ClosetPuzzle) or gv.Bool.Frundarmon_KillK3
			Map.HideByLabel("K3")
		end
	end
	
	void GUARD_CLOSET()
		if ClosetPuzzle
			Map.HideByLabel("CLOSET_GUARD")
		else
			MapText("SLEEPKID."..Map.Leader)
			MapScript.SASKIA("Guard_Closet")
		end
	end
	
	void NPC_CLOSET()
		if ClosetPuzzle
			return
		end
		Events.Annoy("Hey, there's a list of questions on the backside")
		bool r = true
		table gn
		gn = {"boy","girl"}
		for race,gender in pairs ( KidGen )
			bool chk
			string g
			g = gn[math.random(1,2)]
			chk = Events.Yes(sprintf("Is the undead %s kid a %s?",race,g))
			r = r and (g == gender)==chk
			CSayF("Is the undead %s kid a %s? %s/%s[%s] => %s",race,g,tostring(chk),tostring(KidGen[race]==gender),gender,tostring(r))
		end
		ClosetPuzzle = r
		if ClosetPuzzle
			Events.Annoy("You hear a door open in the corridor. But not only that. Some undead children appear to have run into the corridor to guard it!")
			Map.Kill("Barrier",true)
		else
			Events.Annoy("Nothing happens")
		end
	end
	
	void BOSS()
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/UndeadBoss.wav"
		StartCombat.Arena = "Frundarmon"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Foe[1] = "Boss/Frundarmon"
		StartCombat.Ready()
	end
	
	void KIDKILL()
		string old
		old = Map.Layer
		Map.Layer="#001"
		Map.Kill("KID_T11",true)
		Map.Kill("KID_T12",true)
		Map.Layer=old // Bug prevention
	end
	
	void KIDKILLDININGROOM()
		if self.kidkill_diningroom
			return
		end
		Map.Kill("NPC_SASKIA_KID21")
		Map.Kill("NPC_SASKIA_KID22")
		self.kidkill_diningroom = true
	end
	
	
//	void DESTRUCTOR()
//		CSay("Leaving the mansion")
		//self.Lijst.Clear()
//	end
	
	//link DarkDraw = CircularDarkness.Draw__StyleStretch
	//link DarkDraw = CircularDarkness.Draw__StyleSmall
	link DarkDraw = CircularDarkness.Draw1
	
	void Draw()
		self.DarkDraw()
		__yellow
		if self.Kamer!="" && Map.Layer!="#003"
			self.KamerKop[self.Kamer].Draw(math.floor(Graphics.W/2),5,2,0)
		end
	end
	
	void Update()
	end
	
end


Init
	CSay("Entering the mansion")
	//self.Lijst = new TLinkedList
	MScript.Font = GetFont("BIGTEXT")
	MScript.InitKamer("Lobby")
	MScript.InitKamer("Corridor 1st Floor")
	MScript.InitKamer("Living Room")
	MScript.InitKamer("Secret Passage")
	MScript.InitKamer("Storage")
	MScript.InitKamer("Kitchen")
	MScript.InitKamer("Corridor 2nd Floor")
	MScript.InitKamer("Dining Room")
	MScript.InitKamer("Bed Room")
	MScript.InitKamer("Study")
	Map.ZA_Enter("Kitchen","SASKIA:Kitchen")
	Map.ZA_Enter("ToBasement","ToBasement")
	Map.ZA_Enter("Corridor 2nd Floor","Welcome2")
	Map.ZA_Enter("Bed Room","Guard_Closet")
	Map.ZA_Enter("K3","SASKIA:K3")
	if FrunDefeated
		Map.ZA_Enter("Dining Room","KidKillDiningRoom")
	end
	
	if ! FrunDefeated
		Map.ZA_Enter("FrunBoss","SASKIA:Boss")
	end
	//self.boy  = { ["boy"]=true,  ["girl"] = false }
	//self.girl = { ["boy"]=false, ["girl"] = true  }
end
