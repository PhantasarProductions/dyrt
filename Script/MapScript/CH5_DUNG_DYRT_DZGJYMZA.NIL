// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH5_DUNG_DYRT_DZGJYMZA.NIL
// (c) Jeroen Broks, 2020, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 20.07.07
// End License Information



#say "Map Script - None!"
#use "script/use/anyway"
#use "Script/Use/StartCombatMod"
#use "Script/Use/MapScript/CircularDarkness"
#use "Script/Use/MapScript/Memory"
#use "Script/use/Specific/CheckTrueAbyss" as CTA

#macro maxsecrets 2
#macro CURRENT_KAMER self.KamerNames[Map.Layer]

#macro SKIP_DZGJYMZA true

#macro ZO_GAAN_WE_SKIPPEN_OF_NIET if SKIP_DZGJYMZA then Annoy("Skipped boss fight!") return end


#accept MScript

quickmeta group MetaSwitch
	index
		return gv.Bool["DZGJYMZA_KEEP_SWITCH_DONE."..(key:upper())]
	end
	
	newindex
		gv.Bool["DZGJYMZA_KEEP_SWITCH_DONE."..(key:upper())] = value
		for _,n in ipairs { "Jeracko","Rondomo","Aldarus","Kirana" }
			if not MScript.Switch[n]
				CSayF("Exiting: Switch for %s not yet flipped!",n)
				return				
			end
			CSayF("Continueing: Switch for %s has been flipped",n)
		end
		// error("Big doors in the main hall not yet scripted")
		string l
		l = Map.Layer
		Map.Layer="#001"
		Map.Kill("Deur",true)
		Map.Layer = l
	end
end


group MScript
	
	table KamerKop
	table KamerNames
	
	var Font
	
	get string Kamer
		if (not CURRENT_KAMER) or prefixed(CURRENT_KAMER,"END")
			return ""
		end
		// assert(CURRENT_KAMER,"No data for area: "..Map.Layer)
		self.KamerKop[CURRENT_KAMER] = self.KamerKop[CURRENT_KAMER] or self.Font.Text(CURRENT_KAMER)
		return CURRENT_KAMER
	end
	
	link Switch = MetaSwitch
		
	void SWITCH_JERACKO()
		self.Switch.Jeracko = true
	end
	
	void SWITCH_ALDARUS()
		self.Switch.Aldarus = true
	end
	
	void SWITCH_KIRANA()
		self.Switch.Kirana = true
	end
	
	void SHOWSECRET(int i)
		Map.ShowByLabel("SECRET"..i)
	end
	
	void HIDESECRET(int i)
		Map.HideByLabel("SECRET"..i)
	end
	
	link DarkDraw = CircularDarkness.Draw__StyleStretch
	
		// Memory puzzle
	delegate NPC_MEMORY1
	delegate NPC_MEMORY2
	delegate NPC_MEMORY3
	delegate NPC_MEMORY4
	delegate NPC_MEMORY5
	delegate NPC_MEMORY6
	delegate NPC_MEMORY7
	delegate NPC_MEMORY8
	delegate NPC_MEMORY9
	delegate NPC_MEMORY10
	delegate NPC_MEMORY11
	delegate NPC_MEMORY12
	delegate NPC_MEMORY13
	delegate NPC_MEMORY14
	delegate NPC_MEMORY15
	delegate NPC_MEMORY16
	
	link DoneMemory = gv.Bool.Done_DarkStorage_Rondomo_MemoryPuzzle
	
	// Memory
	void RESETMEMORY()
		if self.Switch.Rondomo
			return
		end
		Memory.Init(self,{"Eric","Irravonia","Brendor","Scyndi","Dernor","Merya","Rebecca","Aziella"})
	end
	
	link MEMORYCYCLE = Memory.Cycle
	
	void MemoryPuzzleSolved()
		LoadAudio("SFX/Puzzle/Solved.wav").Play()
		Map.Kill("StartPuzzle",true)
		self.KILLMEMORY()
		Party.LvUp()
	end
	
	void KILLMEMORY()
		for i=1,16
			Map.Kill("NPC_MEMORY"..i,true)
		end
		self.Switch.Rondomo = true
	end
	// End Memory
	
	void SANCTUARY()
		Music.Play("Music/Dungeon/WeniariasTemple.wav")
	end
	
	void LEAVESANCTUARY()
		Music.Play("Music/Dungeon/Dzgjymza.wav")
	end
	
	void NPC_WENIARIA()
		if not Done.Weniaria_in_DzgjymzaKeep_In_Dyrt
			MapScript.SASKIA("Weniaria")
			Map.Layer="#009"
			Map.Kill("DeurDzgjymza",true)
			Map.Layer="#010"
		else
			MapText("WENRECOVER")
		end
		for i=1,8
			var c
			c = RPG.GetChar(RPG.Party[i])
			c.Points( "HP").Have = c.Points( "HP").Maximum
			c.Points("VIT").Have = c.Points("VIT").Maximum		
		end
	end
	
	void NPC_DZGJYMZA()
		MapScript.SASKIA("Dzgjymza")
	end
	
	void DZGJYMZA_START1()
		CSay("Let's start round #1")
		ZO_GAAN_WE_SKIPPEN_OF_NIET
		error("First Dzgjymza fight not yet scripted")
	end
	
	void DZGJYMZA_START2()
		CSay("Let's start round #2")
		ZO_GAAN_WE_SKIPPEN_OF_NIET
		error("Second Dzgjymza fight not yet scripted")
	end
	
	void BANGSET(string o)
		int x
		int y
		Map.Obj = o
		x = Map.ObjInt.x
		y = Map.ObjInt.y
		Map.Obj = "OnyxBang"
		Map.ObjInt.x = x
		Map.ObjInt.y = y
		CSayF("Coordinates for OnyxBang set to (%04d,%04d)",x,y)
	end
	
	void BLENCHY_FADE()
		#- gv.Int.FadeBlenchy 2
		for i=0,9
			Map.Obj = "DS"..i
			Map.ObjInt.Alpha = gv.Int.FadeBlenchy
		end
		CTA.Unlocked_NewGamePlus = true
	end
	
	int  LoveLoop=30
	int  LoveDone=11
	bool LoveFlits
	bool LoveDrawn
	void LOVE()
		if not self.LoveDrawn
			return
		end
		if (self.LoveDone>11 or self.LoveLoop<=0) and (not self.LoveFlits)
			if self.LoveDone<=0 or (self.LoveDone<5 and math.random(0,self.LoveDone)==1)
				gv.Bool.EndLoveStop=true
				return
			end
			--self.LoveDone
			self.LoveLoop=math.random(20,30)
			Map.Obj="Bliksem"
			Map.ObjInt.X = math.random(300,500)
			Map.ObjInt.Rotation = math.random(0,90)-45
			Map.ObjInt.R = math.random(0,255)
			Map.ObjInt.G = math.random(0,255)
			Map.ObjInt.B = math.random(0,255)
		end
		self.LoveFlits = not self.LoveFlits
		Map.Obj="Bliksem"
		Map.ObjBool.Visible = self.LoveFlits
		self.LoveDrawn = false
		--self.LoveLoop
	end
	
	void CONSTRUCTOR()
		UseKettingKaart()
		self.Font = GetFont("BIGTEXT")
		self.KamerNames = {
			["#000"] = "Lobby",
			["#001"] = "Grand Hall",
			["#002"] = "Upstairs Corridor",
			["#003"] = "Wanda's bedroom",
			["#004"] = "Lord Dzgjymza's bedroom",
			["#005"] = "Jeracko's bedroom",
			["#006"] = "Rondomo's bedroom",
			["#007"] = "Aldarus' bedroom",
			["#008"] = "Kirana's bedroom",
			["#009"] = "Passage",
			["#010"] = "Sanctuary",
			["#011"] = "Audience Chamber"			
		}
		for i=1,maxsecrets
			Map.ZA_Enter("SECRET"..i,"SHOWSECRET",i)
			Map.ZA_Leave("SECRET"..i,"HIDESECRET",i)
		end
		Map.ZA_Enter("StartPuzzle","ResetMemory")
		Map.ZA_Enter("Sanctuary","Sanctuary")
		Map.ZA_Leave("Sanctuary","LeaveSanctuary")
	end
	
	void Draw()
		if Map.Layer!="#010" and (not prefixed(Map.Layer,"END"))
			self.DarkDraw()
		else
			self.LoveDrawn = true
		end
		__yellow
		if self.Kamer!=""
			self.KamerKop[self.Kamer].Draw(math.floor(Screen.W/2),5,2,0)
		end		
	end
	
	void Update()
	end
	
end
#say "Map Script - Compiling!"