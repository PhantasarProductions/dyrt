// License:
// **********************************************
// 
// src/Script/Script/MapScript/CH1_DUNG_ExamRuins.scyndi
// (c) Jeroen Broks, 2019-'20, 2025, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// 
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// 
// version: 25.04.22 VIII
// 
// End License


Script

#use "Libs/SCI/Sys"
#use "Libs/SCI/InterVar"

#use "Libs/Music"
#use "libs/collections"
#use "libs/Romannumbers"


#use "Script/Use/Misc/StartCombatMod"
#use "script/use/Need"


// Since MapScripts have their own VMs or "states", this macro only affects this mapscript
#macro hier_puzzleopgelost gBool.Done_Die_Puzzle_Exam_Ruins_Solved

bool killed_exit

group MScript

	var PuzzleCorrect
	var PuzzleUser

	void CreateRebecca()
		var Rebecca
		// Create Rebecca, and at the start she'll always be five levels 
		// higher than her twin brother. She *is* the better warrior after all!
		Party.NewCharacter("Rebecca",RPG.GetChar("Eric").Stat("Level").Value + 5)
		Rebecca = RPG.GetChar("Rebecca")
		Rebecca.Stat("BASE__RESIST_Stat_Fear").Value = 99 / __skill
		Rebecca.Stat("BASE__RESIST_Stat_Exhaustion").Value = math.random(math.floor(9 / __skill),66 / __skill)
		Rebecca.Stat("BASE__RESIST_Stat_Death").Value = math.random(0,math.ceil(99 / __skill))
		Rebecca.Stat("BASE__RESIST_Stat_Destruction").Value = math.random(0,66 / __skill)		
	end
	
	void MUSIC()
		if not Done.IntroRebecca
			Music.Play("Music/Event/Rebecca.wav")
		else
			Music.Play("Music/Dungeon/ExamRuins.wav")
		end
	end
	
	void NPC_IRRAVONIA()
		MapText(sprintf("IRRA_%s",Upper(Field.Leader) ) )
	end
	
	void NPC_ZACK()
		MapText(sprintf("ZACK_%s",Upper(Field.Leader)))
	end
	
	void NPC_SWORDS()
		MapText(sprintf("SWORDS_%s",Upper(Field.Leader)))
	end
	
	void SHOWSECRET()
		CSay("Let's SHOW the secret!")
		Map.ShowByLabel("SECRET_1")
	end
	
	void HIDESECRET()
		CSay("Let's HIDE the secret!")
		Map.HideByLabel("SECRET_1")
	end
	
	void LOBBY2EASTSECTION()
		Map.GoToLayer("#004","From_Lobby")
	end
	
	void EASTSECTION2LOBBY()
		Map.GoToLayer("#001","From_East")
	end
	
	void ENTERNORTHROOM()
		Map.GoToLayer("#005","Start")
	end
	
	void LEAVENORTHROOM()
		Map.GoToLayer("#003","From_North")
	end
	
	void CheckPuzzleCorrect()
		if not(self.PuzzleUser)
			CSay("CheckPuzzleCorrect(): Nothing set so let's get outta here!")
			return 
		end
		bool check1 = true
		bool check2 = true
		for x=0,2		
			for y=0,2
				check1 = check1 and (self.PuzzleUser.GetValue(  x,y) == self.PuzzleCorrect.GetValue(x,y))
				check2 = check2 and (self.PuzzleUser.GetValue(2-y,x) == self.PuzzleCorrect.GetValue(x,y))
				CSayF("Check(%d,%d) => %s",  x,y,tostring(check1))
				CSayF("Check(%d,%d) => %s",2-y,x,tostring(check2))
			end
		end
		if check1 or check2
			hier_puzzleopgelost = true
			Achievements.Award("PUZ_DIEDIE")
			string addswapdata = "// Puzzle\n\nFieldMap.Kthura.Layer='#006\'\n\n";
			for x=0,2	
				for y=0,2
					string tex = 'GFX/Textures/JewelButtons/Blue.png'
					if self.PuzzleUser.GetValue(x,y)
						tex = 'GFX/Textures/JewelButtons/Green.png'
					end
					addswapdata = addswapdata .. sprintf("\tPosition (%d,%d)\n\t\tFieldMap.Kthura.Obj = 'B%d%d'\n\t\tFieldMap.Kthura.ObjString='%s'\n\n",x,y,x+1,y+1,tex)
				end
			end
			addswapdata = "\n\n"
			Map.AddSwap(addswapdata)
			Map.Kill("Puz_Deur",true)
			return 
		end
		return
	end
	
	void PUZ_BUTTON(int x,int y)
		if hier_puzzleopgelost
			return
		end
		CSay("("..type(x)..':'..tostring(x)..", "..type(y)..":"..tostring(y)..")")
		self.PuzzleUser.SetValue(x,y,not self.PuzzleUser.GetValue(x,y))
		Map.Obj = sprintf("B%d%d",x+1,y+1)
		if self.PuzzleUser.GetValue(x,y)
			CSayF("Switch (%d,%d)/%s%d turned on",x,y,lstring.char(x+65),y+1)
			Map.ObjString.Texture = "GFX/Textures/JewelButtons/Green.png"
		else
			CSayF("Switch (%d,%d)/%s%d turned off",x,y,lstring.char(x+65),y+1)
			Map.ObjString.Texture = "GFX/Textures/JewelButtons/Blue.png"
		end
		self.CheckPuzzleCorrect()
	end
	
	void INITPUZZLE()
		// Puzzle solved or generated? Then get outta here!
		if hier_puzzleopgelost or self.PuzzleCorrect
			gv.String.EXAPUZZLESIGN = "We all end up, the remains of the day!"
			return
		end
		int n 
		var PC
		n = math.random(1,6)
		gv.String.EXAPUZZLESIGN = Roman(n)
		self.PuzzleCorrect = new MArray("bool",3,3)
		self.PuzzleUser = new MArray("bool",3,3)
		PC = self.PuzzleCorrect
		switch n
			case 1
				PC.SetValue(1,1,true)
			case 2
				PC.SetValue(0,0,true)
				PC.SetValue(2,2,true)
			case 3
				PC.SetValue(0,0,true)
				PC.SetValue(1,1,true)
				PC.SetValue(2,2,true)
			case 4
				PC.SetValue(0,0,true)
				PC.SetValue(0,2,true)
				PC.SetValue(2,0,true)
				PC.SetValue(2,2,true)
			case 5
				PC.SetValue(0,0,true)
				PC.SetValue(0,2,true)
				PC.SetValue(2,0,true)
				PC.SetValue(2,2,true)
				PC.SetValue(1,1,true)
			case 6
				for i=0,2
					PC.SetValue(0,i,true)
					PC.SetValue(2,i,true)
				end 
			default
				error("Invalid roll: "..n)
		end
		CSayF("Puzzle init roll: %d",n)
		for x=0,2; for y=0,2 
			CSayF("Activate B%d%d (%d,%d)",x+1,y+1,x,y)
			Map.Click(sprintf("B%d%d",x+1,y+1),"PUZ_Button",x,y)
		end; end
	end
	
	void SWITCH_OPEN_BOSS()
		Map.Layer="#001"
		Map.Kill("Deur",true)
		Map.Layer="#007"
		MapText("ENDSW_"..Field.Leader:upper())
	end
	
	void TOBOSS()
		Map.GoToLayer("#008","Start")
	end
	
	void BOSS2LOBBY()
		Map.GoToLayer("#001","FromBossSpot")
	end
	
	void STARTBOSS()
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/Reg.wav"
		StartCombat.Arena = "ExamRuins"
		StartCombat.Foe[1] = "Boss/BigTiger"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
	
	void POSTBOSS()
		// error("No postboss script yet!")
		Map.Show("EricPost")
		Map.Show("ZackPost")
		Map.WalkTo("RebeccaPost")
		Field.Leader = "Rebecca"
		tostring(Done.Done_ExamRuinsCompleted) // Will show nothing, but the trick itself works :P
	end
	
	void STARTZONE()
		if not killed_exit
			if not gv.Bool.Chapter1Complete 
				Map.Kill("Delisto")
			else
				Map.Kill("NPC_Irravonia")
				Map.Kill("NPC_Zack")
			end
			killed_exit = true
		end
	end
	
	void Update()
	end
	
	void Draw()
	end
	
end	
	
Init
	if not Done.CreateRebecca
		MScript.CreateRebecca()
		Respawn.Initiated = false
	end
	Map.ZA_Enter("Enter_SECRET","ShowSecret")
	Map.ZA_Enter( "Show_SECRET","ShowSecret")
	Map.ZA_Enter("Leave_SECRET","HideSecret")
	Map.ZA_Enter( "Hide_SECRET","HideSecret")
	
	Map.ZA_Enter("GoEastJustin","Lobby2EastSection")
	Map.ZA_Enter("To_Lobby",    "EastSection2Lobby")
	Map.ZA_Enter("To_North",    "EnterNorthRoom")
	Map.ZA_Enter("To_South",    "LeaveNorthRoom")
	Map.ZA_Enter("ToBoss",      "ToBoss")
	Map.ZA_Enter("ToLobbyFromBoss","Boss2Lobby")
	
	Map.ZA_Enter("StartZone",   "StartZone")
	
	Map.ZA_Enter("PUZ_Init",    "InitPuzzle")
		
	if not gv.Bool.Chapter1Complete
		Map.ZA_Enter("StartBoss",   "SASKIA:Boss")
	end
end

