// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH3_CITY_Gagolton.nil
// (c) Jeroen Broks, 2020, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 20.10.27
// End License Information

#say "Map Script - None!"
#use "script/use/anyway"
#use "Script/Use/StartCombatMod"
#use "libs/strings"

#macro __EMPTY gv.Bool.GagoltonEmpty
// #macro __EMPTY true

bool gj_checked

group MScript
	
	void DELISTO()
		WorldMap.Delisto()
	end
	
	void GOTO(string tag)
		string loc
		loc = replace(tag,"GOTO_","")
		// That looks awkward above, but replace has a 2nd return and that will spook GoToLayer
		Map.GoToLayer("TOWN",loc)
	end
	
	void MUSIC()
		if __EMPTY
			Music.Play('Music/Cities/Gagolton_Deserted.wav')
		else
			Music.Play('Music/Cities/Gagolton_Populated.wav')
		end
	end
	
	void ENTER(string zone)
		Map.ShowByZone(zone)
		if __EMPTY and (not Done.SeeEmptyGagoltonTown)
			MapText("LEEG")
			WM_Unlocked.D_IndieBush = true
		end
	end
	
	void LEAVE(string zone)
		Map.HideByZone(zone)
	end
	
	void TOBASEMENT()
		if __EMPTY
			Map.GoToLayer("TOWN","Basement")
		else
			Map.WalkTo("BasementExit")
			MapText("GETOUTOFMYLAB")
		end
	end
	
	void NPC_ROSETTA_DISA()
		MapScript.SASKIA("Rosetta")
	end
	
	void Guard()
		MapText("JUGNOENTRY")
	end
	
	get delegate NPC_GUARD_DISA
		return self.Guard
	end
	get delegate NPC_GUARD2_DISA
		return self.Guard
	end
	
	void NPC_DOG_DISA()
		if Map.Leader=="Scyndi"
			MapText("DOG.SCYNDI")
		else
			MapText("DOG")
		end
	end
	
	void NPC_MERCHANT_DISA()
		MapScript.SASKIA("Karl")
	end
	
	void NPC_KIRANA()
		MapScript.SASKIA("Kirana")
	end
	
	void GJ_UNLOCK()
		var GJ
		if (not gv.Bool.GameJolt) or gj_checked or (gj_failed())
			return
		end
		gj_checked = true
		GJ = NIL.Use("Secu/Include/GJSecret.nil")
		if not GJ
			Annoy("Unfortunately I failed to contact Game Jolt, so I could not open the secret passage tied to this area!\n\n- Is your internet connection okay?\n- You never changed your credentials on Game Jolt?\n-Is Game Jolt up and running?\n\nIf you want to try again, leave this dungeon and come back later!")
			return
		end
		if GJ.ImpureRatio>=54
			Map.Kill("GJ_Barrier")
		end
	end
	
	void GJ_BACK()
		Map.Kill("GJ_Barrier")
		gj_checked = true
	end
	
	void TOSUGARLAND()
		Field.LoadMap("GJ_DUNG_Sugarland")
		Map.GoToLayer("#001","Start")
	end
	
	void NPC_ABYSS()
		MapText("RESEARCHNOTES")
		WM_Unlocked.D_Abyss = true
		Map.Kill("NPC_Abyss",true)
	end
	
	void NPC_THIEFDEN()
		MapText("GULLIVER")
		WM_Unlocked.D_THIEFDEN = true 
	end
	
	void CONSTRUCTOR()
		for tag in Map.EachTag
			if __EMPTY and suffixed(tag,"_DISA")
				Map.Kill(tag)
				// Please note that the table used for EachTag won't 
				// change anymore once the for-loop has begun, so this is safe!
				CSayF("Removed NPC: %s",tag)
			elseif prefixed(tag,"GOTO_")
				Map.ZA_Enter(tag,"GoTo",tag)
			elseif prefixed(tag,"ZONE_")
				Map.HideByZone(tag)
				Map.ZA_Enter(tag,"Enter",tag)
				Map.ZA_Leave(tag,"Leave",tag)
			end
		end
		if not(__newgameplus and gv.Bool.AziellaJoined)
			Map.Kill("NPC_ThiefDen")
		end
		Map.ZA_Enter("Delisto2","Delisto")
		Map.ZA_Enter("Delisto3","Delisto")
		Map.ZA_Enter("ToBasement","ToBasement")
		Map.ZA_Enter("GJ_Back","GJ_Back")
		Map.ZA_Enter("GJ_Unlock","GJ_Unlock")
		Map.ZA_Enter("ToSugarland","ToSugarland")
	end
	
	void Draw()
	end
	
	void Update()
	end
	
end
#say "Map Script - Compiling!"
