// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH3_CITY_Independence.nil
// (c) Jeroen Broks, , All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 20.02.24
// End License Information

bool beenonbeach

#say "Map Script - None!"
#use "script/use/anyway"
#use "Script/Use/StartCombatMod"
#use "libs/tables"

group MScript
	void SING()
		var sing
		sing = LoadAudio("SFX/Scenario/CookSing.wav")
		sing.Play()
	end
	
	void SCYNDI()
		gv.String.Scyndi = "Scyndi"
		RPG.GetChar("Scyndi").Name = "Scyndi"
	end
	
	void NPC_TRIXIA()
		MapText("TRIXIA."..(Map.Leader:upper()))
	end
	
	void ROSETTASHOW()
		for i=1,8
			string c
			c = RPG.Party[i]
			if c and c!="" and c!="Kirana"
				Map.ShowObj("Ros"..c)
			end
		end
		Map.HideObj('Player')
	end
	
	void TOBEACH()
		Map.GoToLayer("BEACH","Start")
	end
	
	void TOCITY()
		Map.GoToLayer("CITY","FromBeach")
	end
	
	void AZ_Remove()
		for c in each( { "Eric", "Irravonia", "Brendor", "Scyndi", "Rebecca", "Dernor", "Merya" } )
			Map.Kill("AZ_"..c)
		end
	end
	
	void AZ_REMOVE()
		self.AZ_Remove()
	end
	
	void CreateAziella()
		int tot
		int avg
		int avg2
		for c in each( { "Eric", "Irravonia", "Brendor", "Scyndi", "Rebecca", "Dernor", "Merya" } )
			tot = tot + RPG.GetChar(c).Stat("Level").Value			
		end
		avg = math.ceil(tot/(6 + __skill))
		if (avg<30/__skill)
			avg = 30/__skill
		end
		avg2 = math.min(avg,100)
		// Create Aziella
		var Aziella
		Party.NewCharacter("Aziella",avg)
		Aziella = RPG.GetChar("Aziella")
		Aziella.Stat("BASE__RESIST_Stat_Poison").Value = avg2
		Aziella.Stat("BASE__RESIST_Stat_Disease").Value = avg2
		Aziella.Stat("BASE__RESIST_Stat_Paralysis").Value = avg2
		Aziella.Stat("BASE__RESIST_Stat_Confusion").Value = avg2
		Aziella.Stat("BASE__RESIST_Stat_Sleep").Value = math.random(math.floor(9 / __skill),66 / __skill)
		Aziella.Stat("BASE__RESIST_Stat_Exhaustion").Value = math.random(0,math.ceil(99 / __skill))
		Aziella.Stat("BASE__RESIST_Stat_Death").Value = math.random(0,96 / __skill)		
		Aziella.Stat("BASE__RESIST_Stat_Destruction").Value = math.random(0,66 / __skill)
		for i=0,1
			Aziella.Points(sprintf("SKILLEXP%d",i),true).Have    = avg * (30 / __skill)
			Aziella.Points(sprintf("SKILLLVL%d",i),true).Have    = avg
		end
		RPG.Party[8] = "Aziella"
	end
	
	void AZIELLA()
		if beenonbeach
			return
		end
		beenonbeach = true
		Map.HideByZone("Eat At Joes")
		CSay("Aziella's checkup")
		if not gv.Bool.EntreeAziellaAnnounced
			Map.Kill("NPC_Guard")
			self.AZ_Remove()
			CSay("Aziella's time has not come, so away with the guard!")
			return
		end
		CSay("Guard should remain, but how about Aziella herself?")
		if Done.AziellaJoined
			self.AZ_Remove()
			CSay("Aziella has already joined, so let's remove the base party and get outta here!")
			return
		end
		CSay("Apparently we gotta make Aziella join the group first!")
		self.CreateAziella()
		Map.GoToLayer("Beach","SpotAziella")
		Map.Leader = "Aziella"
		MapScript.SASKIA("Aziella")
	end
	
	void CONSTRUCTOR()
		Map.ZA_Enter("Entree","SASKIA:Entree")
		Map.ZA_Enter("ToBeach","ToBeach")
		Map.ZA_Enter("ToCity","ToCity")
		Map.ZA_Enter("Aziella","Aziella")
	end
	
	void Draw()
	end
	
	void Update()
	end
	
end
#say "Map Script - Compiling!"