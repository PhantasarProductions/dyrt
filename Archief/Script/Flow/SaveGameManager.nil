// License Information:
// src/Tricky Script/Script/Flow/SaveGameManager.nil
// Version: 21.11.25
// Copyright (C) 2019, 2020, 2021 Jeroen Petrus Broks
// 
// ===========================
// This file is part of a project related to the Phantasar Chronicles or another
// series or saga which is property of Jeroen P. Broks.
// This means that it may contain references to a story-line plus characters
// which are property of Jeroen Broks. These references may only be distributed
// along with an unmodified version of the game.
// 
// As soon as you remove or replace ALL references to the storyline or character
// references, or any termology specifically set up for the Phantasar universe,
// or any other univers a story of Jeroen P. Broks is set up for,
// the restrictions of this file are removed and will automatically become
// zLib licensed (see below).
// 
// Please note that doing so counts as a modification and must be marked as such
// in accordance to the zLib license.
// ===========================
// zLib license terms:
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information








#use "script/use/anyway"
#use "script/use/boxes"
#use "libs/linkedlist"
#use "libs/roman"

#macro __GameName Dyrt.NET

global delegate BUB_Draw
global delegate BUB_Update
global delegate BUB_Arrive
global delegate BUB_Load

class SG
	
	static var SmallFont
	static table SkillNames
	static string mode
	static var ZijBalk
	
	static var List
	static int HighID
	
	static table Pics
	static table PicImages
	static bool PicsInitDone
	static table lvlPic
	
	static var BigNumbers
	static var Font
	
	static var Hover
	
	static int LeaveCount
	static string Leave
	
	int ID
	string Location
	int Skill = 2
	string Language
	string Time
	table PartyNames
	table PartyLevels
	static int ScrollY
	var SlotBox
	
	void CONSTRUCTOR()
		self.List = self.List or new TLinkedList // Should already be done, but to make sure!
		self.List.AddLast(self)
		//self.HighID++
		//self.ID = self.HighID
		// Init Pics
		if not self.PicsInitDone
			table TruePics
			int MaxPics
			TruePics = { "Eric","Irravonia","Brendor","Scyndi","Rebecca","Dernor","Merya","Aziella" }
			MaxPics = #TruePics
			setmetatable(self.Pics,{
				["__index"] = string(t,int i)
					int modulo
					modulo = (i % MaxPics)+1
					//if modulo == 0 then modulo = MaxPics end
					return TruePics[modulo]
				end,
				["__newindex"] = void(t,i,v)
					error("SGData.Pics happens to be readonly and therefore no data may be assigned to it!")
				end
			})
			self.PicsInitDone = true
		end
	end

	// Action
	static void Update_Boxes()
		string output
		for item in SG.List.Each
			if item.ID>=0
				output = output .. sprintf("NEW:%d\nLocation:%s\nLanguage:%s\nTime:%s\nSkill:%d\n",item.ID,item.Location,item.Language,item.Time,item.Skill)
				with item
					for i=1,8
						if $PartyNames[i] and $PartyNames[i]!=""
							output = output .. sprintf("CharTag.%d:%s\nCharLvl.%d:%s\n",i,$PartyNames[i],i,$PartyLevels[i])
						end
					end
				end
			end
		end
		output = output .. sprintf("HighID:%d\nScrollY:%d\n",$HighID,$ScrollY)
		while not BubSave.SaveString(output,"Save/__GameName.Directory",true)
			string antwoord
			antwoord = FailureBox("Saving the directory data failed.\nThis data is essential, as bad stuff can happen when this data is not in sync with the savegame files!\n- Do you have write permissions to the folder where this data is saved\n-Do you have write permissions over the file itself?\n- It's also possible there was a little 'hardware hiccup', in that case just try it again!\n- Is the devide on which you store save games still in fully operational state?\nTry to find that out, please!")
			switch antwoord
				case "Abort"
					error("Error saving savegame directory")
				case "Ignore"
					Annoy("I'll try to continue like nothing happened, but be prepared for some permanent damage, and awkward behavior from the savegame directory in the future")					
					GoToFlow("FIELD")
					break
				case "Retry"
					Annoy("Alright, let's try that again!")
				default
					// This should never be possible, but just in case!
					// I've seen crazier things, after all :P
					error("Unknown answer from failurebox!") 				
			end
		end		
	end
	
	static int st_r
	static int st_g
	static int st_b
	static void SaveTalk(string k,infinity)
		//static int r
		//static int g
		//static int b
		string w
		if #({infinity})==0
			w = k
		else
			w = sprintf("SaveTalk: "..k,infinity)
		end
		while self.st_r+self.st_g+self.st_b<100
			self.st_r = (self.st_r+1)%256
			if self.st_r==0 
				self.st_g = (self.st_g+1)%256
				if self.st_g==0
					self.st_b = (self.st_b+1)%256
				end
			end
		end
		CWriteLine(w,$st_r,$st_g,$st_b)
	end
	
	static void SaveData(string File)
		// The reason I put this in a separate function is actually for 
		// my own needs alone, and not those of the player. In order to
		// make debugging easier, I'll require tons of savegames, which
		// I can more easily set up by doing it like this!
		
		// Start
		var jsg
		$SaveTalk("Starting SaveGame Process")
		jsg = BubSave.StartJCRSave(File)
		jsg.Hash = false // NALA uses its own method for this!
		
		// RPG Party Data
		$SaveTalk("Config Party")
		jsg.XTRA = {['CMD']="RPGParty",['PARA']="Party"}
		
		// Swap Data
		$SaveTalk("Swap")
		jsg.XTRA = {['CMD']="Swap", ['PARA']="Swap"}

		// don't serialize yourself, this is more delicate than that!
		jsg.Serial=false
		
		// Map and Player Actor data
		$SaveTalk("Field")
		jsg["Data/Field"] = LuaGetString("FLOW_FIELD","FlowField.SGData","Grab Field Data for SaveGame")
	
		
		// GameVars
		$SaveTalk("GameVars")
		jsg["Data/GameVars"] = gv()
		
		// Inventory
		$SaveTalk("Inventory")
		jsg["Data/Inventory"] = Inventory()
		
		// Abilities
		$SaveTalk("Abilities")
		jsg["Data/MasteredAbilities"] = AbilitiesMastered()
		
		// Security
		if BubSave.SaveXtra("ZZZZZ")
			$SaveTalk("Security")
			jsg.Xtra = {['CMD']="ZZZZZ",['PARA']=""}
		end
		
		// Finalize
		$SaveTalk("Closure!")
		jsg.close = true
		
	end
	
	void Perform_Save()
		int myID
		bool NewSlot
		myID = $ID
		CSay("Prepare to save!")
		if myID<0
			CSay("= Generating new ID")
			myID = $HighID
			$ID=myID
			$HighID++
			CSayF("= ID is now %d",$ID)
			NewSlot=true
		end
		CSay("= Base configuration!")
		$Location = Field.MapData.Title
		$Skill = __skill
		$Language = __lang
		$PartyNames = {}
		$PartyLevels = {}
		CSay("= Character outline")
		if __trueabyss
			$Location = $Location .. sprintf(" - Floor %s",Map.Layer)
		end
		for i=1,8
			if RPG.Party[i] and RPG.Party[i]!=""
				$PartyNames[i] = RPG.Party[i]
				$PartyLevels[i] = RPG.Char(RPG.Party[i]).Stat("Level").Value
			end
		end
		CSay("= Grab the time!")
		$Time = Time.PlayTime
		CSay("= Saving")
		$SaveData(sprintf("Save/__GameName.%d",$ID))
		CSay("= Updating directory!")
		$Update_Boxes()
		if NewSlot
			var newitem
			newitem = new SG
			with newitem
				$ID=-10
				$Location = "<< Create New File >>"
				$SlotBox = new Box(0,0,Screen.Width-40,128)
			end
		end
		$LeaveCount=3
		$Leave="FIELD"
	end
	
	get bool LoginOk
		if not gv.Bool.GAMEJOLT
			CSay("= This game is not set up to contact GJ so let's ignore it all!")
			return false
		end
		if not gv.Bool.GAMEJOLTBYEBYE
			Annoy("Game Jolt credentials were found in this savegame.\nHowever this game will no longer log in to Game Jolt to synchronize achievements with Game Jolt Trophies.\n\nThis is a permanent measure.\n\nSorry for the inconvenience!\n\nP.S. No trying to use older versions of the game won't get you in, so don't even try it!")
			gv.Bool.GAMEJOLTBYEBYE = true
			return false
		elseif true
			return false
		end
		// All this below will be ignored. Sorry!
		if not GameJolt.ByResource("Secu/Sites/GameJolt")
			Annoy("Your savegame was set up to make use of Game Jolt, however, the data I got from this game does not suppor Game Jolt support.\n\nI will let you go on, but no Game Jolt support will be there, sorry!\n\nPlease re-download and re-install the game in order to fix this issue")
			gv.Bool.GAMEJOLT = false
			return false
		end
		bool success
		repeat
			CSay("Trying to login on Game Jolt!")
			success = GameJolt.Login(gv.String.GameJolt_User,gv.String.GameJolt_Token)
			if success
				//gv.Boolean.GameJolt      = true
				//gv.String.GameJolt_User  = self.gju_name
				//gv.String.GameJolt_Token = self.gju_token
				CSay("= Success!")
				return true;
			end
			CSay("= Failed")
			string antwoord
			antwoord = FailureBox("Logging in did not work!\n- Did you enter the correct credentials when you started this game?\n  = User: '"..gv.String.GameJolt_User.."'\n  = Token: '"..gv.String.GameJolt_Token.."'\n- You never changed that after?\n- Are you properly connected to the internet?\n- Are the Game Jolt servers up and running?\nOne of these is likely not in order!")
			switch antwoord
				case "Abort"
					CSay("= Login sequence aborted!")
					return false;
				case "Retry"
					CSay("= User requested to try it again!")
				case "Ignore"
					CSay("= User chose to ignore the error")
					GJLOGINFAILED()
					return true;
				default
					error("Unknown output generated by dialog box")
			end
		forever
	end
	
	
	
	void Perform_Load(string file)
		table Xtra
		var LoadStuff
		CSayF("Loading: %s",file)
		if BubSave.LoadXtra("ZZZZZ")
			CSay("= This file will be verified");
			Xtra[#Xtra+1] = {['CMD']="ZZZZZ",['PARA']=""}
		end
		Xtra[#Xtra+1] = {['CMD']="SWAP",["PARA"]="SWAP"}
		Xtra[#Xtra+1] = {['CMD']="RPGPARTY", ["PARA"]="Party"}
		CSay("= Reading file, and handling NALA core data!")
		LoadStuff = BubSave.LoadFromJCR(file, false, Xtra)
		if LoadStuff==nil then
			Notify("Loading the game has failed!")
			return
		end
		CSay("= Loading GAMEVAR data")
		gvClearAll()
		//assert(NIL.Load(LoadStuff["Data/GameVars"],"Game Variables"))
		LuaDoString("FLOW_SG",NIL.Translate(LoadStuff["Data/GameVars"]) , "Game Variables")
		CSay("= Loading FIELD data")
		LoadFlow("FIELD","Script/Flow/Field.nil")
		LuaDoString("FLOW_FIELD",LoadStuff["Data/Field"])
		CSay("= Loading inventory")
		//assert(NIL.Load(LoadStuff["Data/Inventory"],"Inventory"))
		LuaDoString("FLOW_SG",LoadStuff["Data/Inventory"],"Inventory") // Dirty code straight from hell, but hopefully this does something
		CSay("= Loading lists of mastered abilities")
		if not StateExists("IAA") then LoadState("IAA","Script/Subs/SubIAA.nil") end
		LuaDoString("IAA",LoadStuff["Data/MasteredAbilities"],"Mastered Abilities")
		CSay("= GJ Login => "..tostring(self.LoginOk))
		GoToFlow("FIELD")
		//error("Loading has not yet been implemented!")
	end
	
	void Perform_Delete()
		if $ID<0
			// $ID being lower than zero would mean this is the 'create new savegame' slot
			// Since that slot means the file does not exist, it's completely senseless
			// to even try to delete that (plus it will result in crashses too), so let's
			// butt out, before that happens, shall we?
			return
		end
		error("Deleting has not yet been implemented!")
	end
	

	// Callbacks
	static void Draw()
		// Todo: Saving/Loading screen prior to actual saving/loading
		__white
		// Originally these numbers were used in the ECN bar, but as I don't plan
		// To get that one back, I decided to use these numbers for THIS purpose :)		
		SG.BigNumbers = SG.BigNumbers or LoadImageFont("Fonts/ECN.jfbf")
		// Let's first draw the slots
		int Y
		$Hover = nil;
		for item in SG.List.Each
			if item.ID>=0 or SG.mode == "SAVE"
				item.SlotBox.y = Y - SG.ScrollY
				if item.SlotBox.y>-200 and item.SlotBox.y<Screen.Height+10
					item.SlotBox.Draw()
					if item.ID>=0
						string Pic
						Pic = self.Pics[item.ID]
						__white
						$PicImages[Pic] = $PicImages[Pic] or LoadImage(sprintf("gfx/boxpic/%s/General.png",Pic))
						$PicImages[Pic].Draw(5, (item.SlotBox.y+64)-($PicImages[Pic].Height/2))
						GraphColor.Alpha = 100
						self.BigNumbers.DrawText( tostring(item.ID+1) , 120, item.SlotBox.y+20 )
						GraphColor.Alpha = 255
					end
					if Mouse.X<Screen.Width-40 and Mouse.Y>item.SlotBox.y and Mouse.Y<item.SlotBox.y+128
						$Hover = item
						Color(255,180,0)
					else
						__white
					end
					$Font.DrawText(item.Location,122,item.SlotBox.y+20)
					if item.ID>=0
						$SmallFont.DrawText(sprintf("Lang: %s; %s",item.Language,$SkillNames[item.Skill]),122,item.SlotBox.y+45)
						$SmallFont.DrawText(sprintf("Time: %s",item.Time),122,item.SlotBox.y+65)
					end
					__white
					bool lastlong = false
					for i=1,8
						with item
							string TAG
							int LVL
							int x
							int y
							TAG = $PartyNames[i]
							LVL = $PartyLevels[i]
							if TAG and TAG!=""
								int yplus = 0								
								$lvlPic[TAG] = $lvlPic[TAG] or LoadImage(sprintf("gfx/actors/heroes/%s.South.jpbf/00000000.png",TAG))
								x = (($SlotBox.x+$SlotBox.w)-(50*8))+i*40
								y = ($SlotBox.y+20)								
								$lvlPic[TAG].Draw(x-($lvlPic[TAG].Width/2),y)
								if (lastlong) and $SmallFont.Text(roman(LVL)).Width>32
									lastlong=false
									yplus = 20
								elseif $SmallFont.Text(roman(LVL)).Width>32
									lastlong=true
								else
									lastlong=false
								end
								$SmallFont.DrawText(roman(LVL),x,y+60+yplus,2)
								// $SmallFont.DrawText(tostring($SmallFont.Text(roman(LVL)).Width)..left(tostring(lastlong),1)..tostring(yplus),x,y+80+yplus,2) // debug! Must be on comment when not in use!
								
							end
						end
					end
				end
			end
			Y = Y + 128
		end
		// And now the sidebar
		$ZijBalk.Draw()
		__white
		if $ScrollY>0
			Arrow.Up.Draw($ZijBalk.x+3,$ZijBalk.y+3)
		end
		Arrow.Cancel.Draw($ZijBalk.x+3,$ZijBalk.y+45)
		if $List.Last and $List.Last.MyObject.SlotBox.y+128>Screen.Height
			Arrow.Down.Draw($ZijBalk.x+4,($ZijBalk.y+$ZijBalk.h)-35)
		end
		
		// Mouse Pointer
		__white
		Mouse.Show()
	end
	
	static void ScrollUp()
		if $ScrollY>0
			$ScrollY--
		end
	end
	
	static void ScrollDown()
		var lastbox		
		if not $List.Last
			return
		end
		lastbox = $List.Last.MyObject		
		//CSayF("DEBUG: ID = %03d; y = %04d; y+128 = %04d; Screen Height: %04d",lastbox.ID,lastbox.SlotBox.y,lastbox.SlotBox.y+128,Screen.Height)
		//CSayF("DEBUG: %4d < %4d ??? ",$ScrollY,(lastbox.SlotBox.y+128)-Screen.Height)
		//if $ScrollY<(lastbox.SlotBox.y+128)-Screen.Height
		if (lastbox.SlotBox.y+128)>Screen.Height
			$ScrollY++
		end
	end

	static void Update()
		// debug
		Dev.ConCheck()
		
		// Time
		Time.Update()
		
		// Leave
		if $LeaveCount>0
			Mouse.Sleep=true
			$LeaveCount--
			if $LeaveCount<=0
				GoToFlow($Leave)
				Mouse.Sleep=false
			end
			return
		end
		
		if Mouse.HitLeft 
			if $Hover
				switch $mode:upper()
					case "SAVE"
						$Hover.Perform_Save()
					case "LOAD"
						$Hover.Perform_Load(sprintf("Save/__GameName.%d",$Hover.ID))
				end
			end
			if Mouse.X>$ZijBalk.x and Mouse.Y>$ZijBalk.y+45 and Mouse.Y<$ZijBalk.y+77
				switch $mode:upper()
					case "SAVE"
						GoToFlow("FIELD")
					case "LOAD"
						GoToFlow("MAINMENU")
				end
			end
			
		end
		if Mouse.HeldLeft
			if Mouse.X>$ZijBalk.x
				if Mouse.Y<$ZijBalk.y+40
					$ScrollUp()
				elseif Mouse.Y>($ZijBalk.y+$ZijBalk.h)-40
					$ScrollDown()
				end
			end
		end
		if Keyboard.Held("Up") 
			$ScrollUp()
		elseif Keyboard.Held("Down")
			$ScrollDown()
		end
	end
	
	static bool dbefore

	static void Arrive()
		CSayF("Arrived in the save game manager with mode '%s'",$mode)
		if $mode=="SAVE" and (not $dbefore)
			CSay("= Create NewGame slot for the creation of a new save game file")
			var NewGame
			NewGame = new SG
			with NewGame
				$ID = -1 // Marks a new savegame file
				$Location = "<< CREATE NEW FILE >>"
				$SlotBox = new Box(0,0,Screen.Width-40,128)
				CSayF("= 'NEW FILE' slot created as %d",$ID)
			end
			CSay("= Add this slot to the list!")
			SG.List.AddLast(NewGame)
			CSay("= Dupe removal")
			SG.List.RemoveDupes()
			$dbefore=true
		end

	end
	
	static void Load()
		CSay("Welcome to the save game manager! I need to configure some things first")
		Box.NeverList = true
		CSay("= Side Bar")
		SG.ZijBalk = new Box(Screen.Width-40,0,40,Screen.Height)
		CSay("= Create list of slots")
		SG.List = new TLinkedList
		CSay("= Check directory file: Save/__GameName.Directory")
		if BubSave.Exists("Save/__GameName.Directory")
			CSay("= Since it exists! Let's load it!")
			table sgdirLines
			sgdirLines = BubSave.LoadLines("Save/__GameName.Directory")
			CSay("= Got it, let's not break it all down!")
			CSay(type(sgdirLines))
			var NSG
			for i,line in ipairs(sgdirLines)
				Dev.Log(sprintf("line %i > %s",i,line))
				table sp
				string cmd
				string para
				sp = { "","" }
				int p = 1
				if trim(line)!=""
					for j=1,#line
						string ch
						ch = Mid(line,j,1)
						if ch==":" and p!=2
							p=2
						else
							sp[p] = sp[p] .. ch
						end
					end				
					cmd=sp[1]
					para=sp[2]
					switch cmd
						case "NEW"
							NSG = new SG
							NSG.ID = tonumber(para);
							NSG.SlotBox = new Box(0,0,Screen.Width-40,128)
						case "Location"
							NSG.Location = para
						case "Language"
							NSG.Language = para
						case "Time"
							NSG.Time = para
						case "Skill"
							NSG.Skill = tonumber(para) or 2
						case "CharTag.1" "CharTag.2" "CharTag.3" "CharTag.4" "CharTag.5" "CharTag.6" "CharTag.7" "CharTag.8"
							int idx
							idx = tonumber(Right(cmd,1))
							NSG.PartyNames[idx] = para
						case "CharLvl.1" "CharLvl.2" "CharLvl.3" "CharLvl.4" "CharLvl.5" "CharLvl.6" "CharLvl.7" "CharLvl.8"
							int idx
							idx = tonumber(Right(cmd,1))
							NSG.PartyLevels[idx] = tonumber(para) or 1
						case "HighID"
							SG.HighID = tonumber(para)
						case "ScrollY"
							SG.ScrollY = tonumber(para)
						default
							error("Unknown directory command: "..cmd.." in line "..i.."\n\nMessed with the savegame directory file?")
					end
				end
			end
		else
			CWriteLine("NOTE: The __GameName.Directory file does not yet exist! If no savegames have been made before this is normal",255,0,0)
		end
		CSay("= Obtain font!")
		SG.Font = GetFont("BOXTEXT")
		CSay("= Dupe removal")
		SG.List.RemoveDupes()
		CSay("= Small Font")
		$SmallFont = LoadImageFont("Fonts/Coolvetica.12.jfbf")
		CSay("= Skill Names")
		$SkillNames[1] = "Easy mode"
		$SkillNames[2] = "Casual mode"
		$SkillNames[3] = "Hard mode"
		CSay("All done, let's move on to the next step!")		
	end
end


BUB_Draw=SG.Draw
BUB_Update=SG.Update
BUB_Arrive=SG.Arrive
BUB_Load=SG.Load