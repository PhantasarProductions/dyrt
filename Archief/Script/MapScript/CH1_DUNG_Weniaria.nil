// License Information:
// **********************************************
// 
// src/Tricky Script/Script/MapScript/CH1_DUNG_Weniaria.nil
// (c) Jeroen Broks, 2019, 2020, All Rights Reserved.
// 
// This file contains material that is related
// to a storyline that is which is strictly
// copyrighted to Jeroen Broks.
// 
// This file may only be used in an unmodified
// form with an unmodified version of the
// software this file belongs to.
// 
// You may use this file for your study to see
// how I solved certain things in the creation
// of this project to see if you find valuable
// leads for the creation of your own.
// 
// Mostly this file comes along with a project
// that is for most part released under an
// open source license and that means that if
// you use that code with this file removed
// from it, you can use it under that license.
// Please check out the other files to find out
// which license applies.
// This file comes 'as-is' and in no possible
// way the author can be held responsible of
// any form of damages that may occur due to
// the usage of this file
// 
// 
// **********************************************
// 
// version: 20.09.17
// End License Information

#macro __weniaria_spoken gv.Bool.Done_Scyndi_Prologue_Spoken_To_Weniaria_To_Open_The_Door
#macro __weniaria_complete gv.Bool.Done_Scyndi_Prologue_Complete
#macro __weniaria_boss gv.Bool.Done_Scyndi_Boss_Wenaria_Dead

#say "Map Script - None!"
#use "script/use/anyway"
#use "Script/Use/StartCombatMod"

bool cleaned

group MScript
	
	void HS1()
		Map.HideByLabel("SECRET_1")
	end
	
	void SS1()
		Map.ShowByLabel("SECRET_1")
	end
	
	void HUGE()
		if not Done.Done_Weniaria_Statue_Huge
			MapText("HUGE")
		end
	end
	
	void NPC_WENIARIA()
		if not __weniaria_spoken
			__weniaria_spoken = true
			MapText("STATUEEVIL")
			Map.Kill("WenDeur")
			return
		end
		if not __weniaria_complete
			MapText("STATUEEVILB")
			return
		end
		MapText("STATUEGENERAL")
		for i=1,8
			if RPG.Party[i] and RPG.Party[i]!=""
				var ch
				ch = RPG.GetChar(RPG.Party[i])
				ch.Points("HP").Have = ch.Points("HP").Maximum
				ch.Points("VIT").Have = ch.Points("VIT").Maximum
			end
		end
	end
	
	void SWITCH_SW3()
		Map.Kill("Deur3",true)
	end
	
	void CLEANUP()
		if gv.Bool.Chapter1Complete
			return
		end
		if not cleaned
			if not __weniaria_complete
				Map.Kill("NPC_SASKIA_Priest")
				if __weniaria_spoken and (not __weniaria_complete)
					Map.Kill("WenDeur")
				end
			end
		cleaned = true
		end
	end
	
	
	void STARTBOSS()
		StartCombat.StartSession()
		StartCombat.Music = "Music/Combat/Boss/Special.Wav"
		StartCombat.Arena = "Weniaria"
		StartCombat.Foe[1] = "Boss/ShadowSweeper"
		StartCombat.NoLose = false
		StartCombat.FoeIDDQD = false
		StartCombat.Ready()
	end
	
	void BOSSBACK()
		if __weniaria_boss
			MapScript.SASKIA("Prison")
		else
			__weniaria_complete = true
			Map.GoToLayer("#006","Einde")
		end
	end
	
	void LEAVETEMPLE()
		if gv.Bool.Chapter1Complete
			WorldMap.Delisto()
		end
	end
	
	void CONSTRUCTOR()
		Map.ZA_Enter("D_SECRET1","SS1")
		Map.ZA_Leave("D_SECRET1","HS1")
		Map.ZA_Enter("HUGE","Huge")
		Map.ZA_Enter("CleanUp","CleanUp")
		Map.ZA_Enter("ActBoss","SASKIA:Boss")
		Map.ZA_Enter("BossBack","BossBack")
		Map.ZA_Enter("LeaveTemple","LeaveTemple")
		if not Done.EnterWeniariaDuringScyndiPrologue
			gv.Bool["RESPAWN_INITIATED"] = false
		end
		if __newgameplus and (not Done.ScyndiStartWithAllGodsInNewGamePlus)
			CSay("Setting Seelah Gandra's skills right for New Game +")
			for i=1,4 do
				RPG.GetChar("Scyndi").Points("SKILLLVL"..i).Have=1
			end
		end
	end
	
	void Draw()
	end
	
	void Update()
	end
	
end
#say "Map Script - Compiling!"